# Maintainer: Alastair Hughes < hobbitalastair at yandex dot com>

# Package information
pkgname='file'
pkgver='5.26'
pkgrel=1
pkgdesc='File type identification utility'

# Additional information...
arch=('mips' 'i586')
url='http://www.darwinsys.com/file/'
license=('custom')

# Dependency information
groups=('base')
makedepends=('zlib' 'musl')
hostdepends=('toolchain')

# Building information
source=("ftp://ftp.astron.com/pub/$pkgname/$pkgname-$pkgver.tar.gz")
md5sums=('bdd12400b90b78ce3c57e79c8d4e0e17')


build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    #TODO: Fix workaround here (patch upstream?)
    ./configure --prefix="/usr" \
        --host="$(echo "${_target_triplet}" | sed 's:musl:gnu:')" \
        --build="${_local_triplet}" \
        --disable-shared
    make
}


check() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    make -k check
}


package() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    make DESTDIR="${pkgdir}/" install
    install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
    rmdir "${pkgdir}/usr/share/man/man5"
}

