# Maintainer: Alastair Hughes < hobbitalastair at yandex dot com>

# Package information
pkgname='parted'
pkgver='3.2'
pkgrel=46
pkgdesc="A program for creating, destroying, resizing, checking and copying partitions"

# Additional information...
arch=('mips' 'i586')
url="http://www.gnu.org/software/parted/parted.html"
license=('GPL3')

# Dependency information
depends=('e2fsprogs' 'readline')

# Building information
source=("http://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.xz"
        "fix-includes.patch"
        "devmapper.patch")
md5sums=('0247b6a7b314f8edeb618159fa95f9cb'
         'e87ffa1171acbebfa9d2475a093e7e64'
         '83d297bd20a11b47a4239b101bc26ad2')


prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    # TODO: Upstream? - fix-includes from Alpine Linux.
    patch -p1 < "${srcdir}/fix-includes.patch"

    # TODO: Upstream? - devmapper patch from Funtoo Linux (from LFS site...).
    patch -p1 < "${srcdir}/devmapper.patch"
}


build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    ./configure --prefix="/usr" \
        --sbindir="/usr/bin" \
        --disable-debug \
        --disable-rpath \
        --disable-device-mapper
    make
}



package() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    make DESTDIR="${pkgdir}/" install

    # We don't include info files.
    rm -rf "${pkgdir}/usr/share/info"
    rm -f "${pkgdir}/usr/lib/charset.alias"
}
