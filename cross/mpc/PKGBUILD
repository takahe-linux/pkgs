# Maintainer: Alastair Hughes < hobbitalastair at yandex dot com>

# Package information
pkgname='mpc'
pkgver='0.r1332.608a221'
pkgrel=1
pkgdesc="Library for the arithmetic of complex numbers with arbitrarily high precision"

# Additional information...
arch=('mips' 'i586')
url="http://www.multiprecision.org/"
license=('LGPL')

# Dependency information
depends=('mpfr' 'gmp' 'musl')
hostdepends=('toolchain' 'git')

# Building information
source=("${pkgname}::git+https://gforge.inria.fr/anonscm/git/mpc/mpc.git#commit=608a22178bea8fe9bebab88b5bc23a914f0926da")
md5sums=('SKIP')


pkgver() {
    cd "${srcdir}/${pkgname}"
    printf "0.r%s.%s" "$(git rev-list --count HEAD)" \
        "$(git rev-parse --short HEAD)"
}


prepare() {
    cd "${srcdir}/${pkgname}"
    autoreconf -i
}


build() {
    cd "${srcdir}/${pkgname}"

    ./configure --prefix="/usr" \
        --build="${_local_triplet}" \
        --host="${_target_triplet}" \

    make
}


package() {
    cd "${srcdir}/${pkgname}"

    make DESTDIR="${pkgdir}/" install

    # Remove info files 
    rm -rf "${pkgdir}/usr/share/info"
}
