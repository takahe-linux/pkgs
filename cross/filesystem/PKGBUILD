# Maintainer: Alastair Hughes < hobbitalastair at yandex dot com>

# Package information
pkgname='filesystem'
pkgver='2017.08'
pkgrel=1
pkgdesc='Base filesystem'

# Additional information...
arch=('any')
url='https://bitbucket.org/Alastair_Hughes/takahe-linux'
license=('MIT')

# Dependency information
groups=('base' 'base-devel')

# Building information
backup=('etc/group'
        'etc/gshadow'
        'etc/passwd'
        'etc/shadow'
        'etc/shells'
        'etc/profile'
        'etc/issue')
source=('group'
        'gshadow'
        'issue'
        'passwd'
        'shadow'
        'shells'
        'profile')
md5sums=('803da7c3c9df9b47a78b52fe9ddf02b1'
         '822b75f0faca19a9c4cee334c63ab1b3'
         '58233879530afb7b1c6a8e51a14d3433'
         'a9d43e66298f11c8e997268f6746b99b'
         '7a4d75479bc052c6dbe44ac957cb19d3'
         'a0369066c5193f25f80cf0f679349008'
         'aba85e21458ac8322c619646604bba2e')

package() {
    cd ${pkgdir}

    # Setup root filesystem
    for d in boot dev etc home mnt usr var run; do
        install -d -m755 ${d}
    done
    install -d -m555 proc
    install -d -m555 sys
    install -d -m0750 root
    install -d -m1777 tmp

    # Setup /etc
    install -d ${pkgdir}/etc/profile.d
    for file in group issue passwd shells profile; do
        install -m644 "${srcdir}/${file}" "${pkgdir}/etc"
    done

    ln -s "/proc/self/mounts" "${pkgdir}/etc/mtab"
    for file in shadow gshadow; do
        install -m600 "${srcdir}/${file}" "${pkgdir}/etc"
    done

    # Setup /var
    for d in log/old; do
        install -d -m755 var/${d}
    done

    install -d -m1777 var/tmp
    ln -s ../run var/run
    ln -s ../run/lock var/lock
    install -d -m755 /run/lock

    # Basically everything needs at least /bin/sh; add the /bin symlink.
    ln -s usr/bin bin

    # Setup /usr hierarchy
    for d in bin include lib share/misc; do
        install -d -m755 usr/${d}
    done
    for d in $(seq 8); do
        install -d -m755 usr/share/man/man${d}
    done
}
