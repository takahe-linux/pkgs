# Maintainer: Alastair Hughes < hobbitalastair at yandex dot com>

# Package information
pkgname='musl'
pkgver='1.1.15'
pkgrel=2
pkgdesc="Lightweight libc implementation"

# Additional information...
arch=('i586' 'mips')
url="http://www.musl-libc.org"
license=('MIT')

# Dependency information
groups=('base-devel')
makedepends=('linux-api-headers')
hostdepends=("${_target_triplet}-gcc-static" "${_target_triplet}-binutils")

# Building information
source=("http://www.musl-libc.org/releases/${pkgname}-${pkgver}.tar.gz"
        "mips-termios.patch")
md5sums=('9590a9d47ee64f220b3c12f7afb864ca'
         '76752ed7a3194b2931317bcfac1f63b7')


prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    # MIPS termios fix (http://www.openwall.com/lists/musl/2016/07/13/3)
    patch -p1 < "${srcdir}/mips-termios.patch"
}


build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    ./configure --target="${_target_triplet}" \
                --prefix="/usr" \
                --syslibdir="/usr/lib" \
                --libdir="/usr/lib" \
                --bindir="/usr/bin" \
                --includedir="/usr/include" \
                --disable-shared

    make
}


package() {
    cd "${pkgname}-${pkgver}"

    # Install musl
    make DESTDIR="${pkgdir}" install
}
