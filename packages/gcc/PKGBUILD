# Maintainer: Alastair Hughes < hobbitalastair at yandex dot com>

# This needs more than 1.4GB of spare space to build!

# Package information
pkgname='gcc'
pkgver='5.3.0'
pkgrel=1
pkgdesc="The GNU Compiler Collection"

# Additional information...
arch=('mips' 'i586')
url="http://gcc.gnu.org"
license=('GPL' 'LGPL' 'FDL')

# Dependency information
groups=('base-devel')
# Depends: packages/musl packages/gmp packages/mpfr packages/mpc
# Depends: packages/binutils targets/toolchain
makedepends=('libc' 'gmp' 'mpfr' 'mpc' 'binutils')

# Building information
options=('!emptydirs')
source=("ftp://gcc.gnu.org/pub/gcc/releases/gcc-${pkgver}/gcc-${pkgver}.tar.bz2"
        *.patch)
sha256sums=('b84f5592e9218b73dbae612b5253035a7b34a9a1f7688d2e1bfaaf7267d5c4db'
            '0b3a2ca007dda01583b4a4d22e9060f5f771deb7099312a2f0dab679f676386d'
            'fe3334cffd41d3becfe3e57bea38822678b463a0947a3141195d953aab8d26e8'
            'dddd08b5097ffc0008e36a3bcfe00a54fbc46684b265b7ff6e683d9396a6c6d0'
            '7ad65c8a8613b34b7ffdf0dcee15659ba12a21ed0b65052644d6ca73653d337a'
            '9854b80d6b4854f48438f6784e86411fbc607763a6be80faae5a36c508c3cf9e'
            'e0f0c2d556ab5e0367f579307db1b1b5abfc4533ccf423597eabcefae52430af'
            'c00abb172cd9aee0c951450b4e77d320ea6fd76a2901e96547113c3d0e9bf210')


prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    echo "${pkgver}" > gcc/BASE-VER

    # hack! - some configure tests for header files using "$CPP $CPPFLAGS"
    sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" {libiberty,gcc}/configure

    # Disable fixincludes (could lead to header pollution)
    sed -i 's@\./fixinc\.sh@-c true@' gcc/Makefile.in

    # Apply the patches...
    for p in "${srcdir}"/*.patch; do
        patch -p1 < "${p}"
    done
    
    mkdir "${srcdir}/gcc-build"

}


build() {
    cd "${srcdir}/gcc-build"

    #TODO: Figure out a more suitable way of doing this... some of these
    # flags break the build (CFLAGS, I think - because we still need to compile
    # host binaries here)
    unset CFLAGS CXXFLAGS CFLAGS_FOR_TARGET CXXFLAGS_FOR_TARGET LDFLAGS \
        LDFLAGS_FOR_TARGET

    "${srcdir}/${pkgname}-${pkgver}/configure" \
        --build="${_local_triplet}" \
        --host="${_target_triplet}" \
        --target="${_target_triplet}" \
        --prefix="/usr" \
        --libdir="/usr/lib" \
        --libexecdir="/usr/lib" \
        --mandir="/usr/share/man" \
        --enable-languages=c,c++ \
        --enable-checking=release \
        --enable-__cxa_atexit \
        --enable-threads=posix \
        --enable-libstdcxx-time \
        --enable-static \
        --disable-nls \
        --disable-shared \
        --disable-host-shared \
        --disable-multilib \
        --disable-werror \
        --disable-libstdcxx-pch \
        --disable-libatomic \
        --disable-libsanitizer \
        --disable-libgomp \
        --disable-libquadmath \
        --disable-libvtv \
        --disable-libssp \
        --disable-lto \
        --disable-libunwind-exceptions \
        --with-native-system-header-dir="/usr/include" \
        --with-local-prefix="/usr" \
        --with-gnu-as \
        --with-gnu-ld \
        --with-system-zlib

    make AS_FOR_TARGET="${AS}" \
        LD_FOR_TARGET="${LD}"
}


package() {
    cd "${srcdir}/gcc-build"

    make DESTDIR="${pkgdir}" install -j1

    # strip target binaries
    find "${pkgdir}/usr/lib/gcc/${_target_triplet}/" \
         "${pkgdir}/usr/lib" \
         -type f -and \( -name \*.a -or -name \*.o \) \
         -exec "${_toolroot}/bin/${_target_triplet}-objcopy" \
         -R .comment -R .note -R .debug_info -R .debug_aranges \
         -R .debug_pubnames -R .debug_pubtypes -R .debug_abbrev -R .debug_line \
         -R .debug_str -R .debug_ranges -R .debug_loc '{}' \;

    # Remove info files (already existing)
    rm -r "${pkgdir}/usr/share/info"
}
