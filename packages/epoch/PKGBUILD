# Maintainer: Alastair Hughes < hobbitalastair at yandex dot com>

# Package information
pkgname='epoch'
pkgver='1.3.0'
pkgrel=1
pkgdesc="Basic init system with minimal dependencies"

# Additional information...
arch=('mips')
url="http://universe2.us/epoch.html"
license=('custom') # Public domain

# Dependency information
groups=('base')
# Depends: packages/musl packages/bash packages/linux-api-headers
# Depends: targets/toolchain
depends=('sh')
makedepends=('libc' 'linux-api-headers')
provides=("init")
conflicts=("init")

# Building information
#TODO: Make the backup file /etc/... minus the leading /
backup=("etc/epoch/epoch.conf")
source=("http://universe2.us/${pkgname}_${pkgver}.tar.gz"
    "epoch.conf")
md5sums=('30bfaeee48a42fa22a83bef57e3b15c7'
         '6c69dd37789145fe80e5e6a6fc775fd4')


package() {
    cd "${srcdir}/${pkgname}_${pkgver}"

    EPOCH_OPTS="--configdir /etc/epoch/ \
        --logdir /var/log \
        --binarypath /usr/bin/epoch \
        --ldflags ${_target_ldflags} \
        --cc ${CC} \
        --shellpath /usr/bin/sh \
        --disable-backtraces"

    CFLAGS="${_target_cflags} -static" ./buildepoch.sh $EPOCH_OPTS

    # Install the built programs and symlinks
    install -d -m0755 "${pkgdir}/usr/bin"
    mv built/sbin/* "${pkgdir}/usr/bin"
    # Add a new wall symlink
    ln -s "/usr/bin/epoch" "${pkgdir}/usr/bin/wall"

    # Copy the license file across
    install -D -m644 UNLICENSE.TXT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

    # Install the default config
    install -D -m644 "${srcdir}/epoch.conf" "${pkgdir}/etc/epoch/epoch.conf"

}
