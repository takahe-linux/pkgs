# Maintainer: Alastair Hughes < hobbitalastair at yandex dot com>

# Package information
pkgname='poppler'
pkgver='0.66.0'
pkgrel=3
pkgdesc="PDF rendering library and toolkit"

# Additional information...
arch=('mips' 'i586')
url="https://poppler.freedesktop.org/"
license=('GPL')

# Dependency information
depends=('libpng' 'libjpeg' 'fontconfig' 'freetype' 'expat' 'zlib')
makedepends=('cmake')

# Building information
source=("${url}/${pkgname}-${pkgver}.tar.xz")
md5sums=('6602813e52f02f85f11ba1590e0ef749')


build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    cmake . -Bbuild -DCMAKE_INSTALL_PREFIX="/usr" -DENABLE_LIBOPENJPEG="none"
    make -C build
}


package() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    make -C build DESTDIR="${pkgdir}/" install
}
