# Maintainer: Alastair Hughes < hobbitalastair at yandex dot com>

# Package information
pkgname='libnsfb'
pkgver='0.1.5'
pkgrel=41
pkgdesc="Framebuffer abstraction library from the Netsurf project"

# Additional information...
arch=('mips' 'i586')
url="http://www.netsurf-browser.org/"
license=('MIT' 'GPL2')

# Dependency information
depends=('sdl')
makedepends=('git' 'netsurf-buildsystem')

# Building information
source=("git://git.netsurf-browser.org/${pkgname}.git#tag=release/${pkgver}"
        "alloc_check.patch")
md5sums=('SKIP'
         '11d7fc214384ab4f03df3b052ae89acf')


prepare() {
    cd "${srcdir}/${pkgname}"
    patch -p1 < "${srcdir}/alloc_check.patch"
}


build() {
    cd "${srcdir}/${pkgname}"
    make LIBDIR=lib INCLUDEDIR=include PREFIX=/usr COMPONENT_TYPE="lib-shared"
}


package() {
    cd "${srcdir}/${pkgname}"

    make LIBDIR=lib INCLUDEDIR=include PREFIX=/usr COMPONENT_TYPE="lib-shared" \
        DESTDIR="${pkgdir}/" install
}
