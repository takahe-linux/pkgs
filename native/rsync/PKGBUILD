# Maintainer: Alastair Hughes < hobbitalastair at yandex dot com>

# Package information
pkgname='rsync'
pkgver='3.1.2'
pkgrel=7
pkgdesc="A fast differential file transfer program"

# Additional information...
arch=('mips' 'i586')
url="https://rsync.samba.org/"
license=('GPL3')

# Dependency information
depends=('perl' 'zlib')

# Building information
source=("${url}/ftp/rsync/${pkgname}-${pkgver}.tar.gz")
md5sums=('0f758d7e000c0f7f7d3792610fad70cb')


build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    ./configure --prefix="/usr" \
        --disable-acl-support \
        --disable-xattr-support \
        --disable-debug
    make
}


package() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    make DESTDIR="${pkgdir}/" install
}
