# Maintainer: Alastair Hughes < hobbitalastair at yandex dot com>

# Package information
pkgname='make'
pkgver='4.2.1'
pkgrel=10
pkgdesc="GNU make utility to maintain groups of programs"

# Additional information...
arch=('mips' 'i586')
url="http://www.gnu.org/software/make"
license=('GPL3')

# Dependency information
groups=('base-devel')
depends=('hush')
makedepends=('musl')
hostdepends=('toolchain')

# Building information
source=("ftp://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.bz2")
md5sums=('15b012617e7c44c0ed482721629577ac')


build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    ./configure --prefix="/usr" \
        --host="${_target_triplet}" \
        --build="${_local_triplet}" \
        --disable-load
    make
}


check() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    make -k check
}


package() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    make DESTDIR="${pkgdir}/" install

    rm -rf "${pkgdir}/usr/share/info"
}
