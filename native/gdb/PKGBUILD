# Maintainer: Alastair Hughes < hobbitalastair at yandex dot com>

# Package information
pkgname='gdb'
pkgver='8.1.1'
pkgrel=1
pkgdesc='The GNU Debugger'

# Additional information...
arch=('mips' 'i586')
url='http://www.gnu.org/software/gdb/'
license=('GPL3')

# Dependency information
depends=('readline' 'zlib')

# Building information
backup=('etc/gdb/gdbinit')
source=("http://ftp.gnu.org/gnu/gdb/${pkgname}-${pkgver}.tar.xz"
        "gdb-sgidefs.h"
        "gdb-linux_nat.patch")
sha1sums=('8c04c12f11b5ba74e7506785632c7fc593ead90f'
          '20eb85f7adb77b894229998432caae530962316c'
          '7916f25d3a6ea7830736f76b8ddc6e445fd26fdd')


prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    # hack! - libiberty configure tests for header files using "$CPP $CPPFLAGS"
    sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure

    cp "${srcdir}/gdb-sgidefs.h" "./gdb/sgidefs.h"

    patch -p0 < "${srcdir}/gdb-linux_nat.patch"
}


build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    ./configure --prefix="/usr" \
        --disable-plugins \
        --disable-multilib \
        --disable-werror \
        --disable-nls \
        --disable-gdbserver \
        --with-system-gdbinit=/etc/gdb/gdbinit \
        --with-system-readline \
        --with-system-zlib

    # Disable building the documentation using MAKEINFO=true.
    # Apparently some GDB releases have messed up timestamps causing the
    # documentation to be regenerated, which fails as we don't have MAKEINFO.
    # Instead, just set MAKEINFO="true".
    make MAKEINFO="true"
}


package() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    make MAKEINFO="true" DESTDIR="${pkgdir}/" install

    # Install "custom" system gdbinit.
    install -dm755 "${pkgdir}/etc/gdb"
    touch "${pkgdir}/etc/gdb/gdbinit"

    # Resolve conflicts with binutils.
    rm "${pkgdir}/usr/include"/{ansidecl,bfd,bfdlink,dis-asm,symcat}.h
    rm -r "${pkgdir}/usr/share/info"
    rm "${pkgdir}/usr/lib"/{libbfd,libopcodes}.a

    # Remove /usr/lib as we don't need the remaining library, and charset.alias
    # conflicts with gettext.
    rm -rf "${pkgdir}/usr/lib"
}
