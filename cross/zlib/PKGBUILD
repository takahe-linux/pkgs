# Maintainer: Alastair Hughes < hobbitalastair at yandex dot com>

# Package information
pkgname='zlib'
pkgver='1.2.11'
pkgrel=8
pkgdesc='Compression library implementing the deflate compression method found in gzip and PKZIP'

# Additional information...
arch=('i586' 'mips')
url="http://www.zlib.net/"
license=('custom')

# Dependency information
makedepends=('musl')
hostdepends=('toolchain')

# Building information
source=("http://zlib.net/current/zlib-${pkgver}.tar.gz")
md5sums=('1c9f62f0778697a09d36121ead88e08e')


prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    # Extract the the license from zlib.h
    echo "Extracting license..."
    grep -A 26 '^Copyright' README > LICENSE
}


build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    ./configure --prefix="/usr" --static
    make
}


package() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    make DESTDIR="${pkgdir}/" install

    # Install the license
    install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
