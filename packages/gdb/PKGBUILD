# Maintainer: Alastair Hughes < hobbitalastair at yandex dot com>

# Package information
pkgname='gdb'
pkgver='7.11.1'
pkgrel=1
pkgdesc='The GNU Debugger'

# Additional information...
arch=('mips' 'i586')
url='http://www.gnu.org/software/gdb/'
license=('GPL3')

# Dependency information
groups=('base-devel')
depends=('ncurses')
makedepends=('musl')
hostdepends=('toolchain')

# Building information
backup=('etc/gdb/gdbinit')
source=("http://ftp.gnu.org/gnu/gdb/${pkgname}-${pkgver}.tar.xz"
        "gdb-sgidefs.h"
        "gdb-linux_nat.patch"
        "gnulib-configure.patch")
sha1sums=('df23fde077df1b8555949281bc963596f70de3ec'
          '20eb85f7adb77b894229998432caae530962316c'
          '7a60c126b1a49cfb554b318a8d7aee1d79fe2bfe'
          'abb3f47ae79c825f8aa76bbb9628afd864a26442')


prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    # hack! - libiberty configure tests for header files using "$CPP $CPPFLAGS"
    sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure

    cp "${srcdir}/gdb-sgidefs.h" "./gdb/sgidefs.h"

    patch -p1 < "${srcdir}/gdb-linux_nat.patch"

    # Work around gnulib trying to use rpl_gettimeofday, which is undefined.
    patch gdb/gnulib/configure < "${srcdir}/gnulib-configure.patch"
}


build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    #TODO: Add --with-system-readline, once I've actually *packaged* readline.

    ./configure --prefix="/usr" \
        --prefix="/usr" \
        --build="${_local_triplet}" \
        --host="${_target_triplet}" \
        --target="${_target_triplet}" \
        --disable-multilib \
        --enable-static \
        --disable-werror \
        --disable-shared \
        --disable-nls \
        --with-system-gdbinit=/etc/gdb/gdbinit
    make
}


package() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    make DESTDIR="${pkgdir}/" install

    # install "custom" system gdbinit
    install -dm755 "${pkgdir}/etc/gdb"
    touch "${pkgdir}/etc/gdb/gdbinit"

    # resolve conflicts with binutils
    rm "${pkgdir}/usr/include"/{ansidecl,bfd,bfdlink,dis-asm,plugin-api,symcat}.h
    rm -r "${pkgdir}/usr/share/info"
    rm "${pkgdir}/usr/lib"/{libbfd,libopcodes}.a

    # resolve conflicts with gettext...
    rm "${pkgdir}/usr/lib/charset.alias"
}
