# Maintainer: Alastair Hughes < hobbitalastair at yandex dot com>

# Package information
_pkgname='binutils'
pkgname="${_target_triplet}-${_pkgname}"
pkgver='2.31.1'
pkgrel=1
pkgdesc="A set of programs to assemble and manipulate binary and object files"

# Additional information...
arch=('x86_64')
url="http://www.gnu.org/software/${_pkgname}/"
license=('GPL')

# Dependency information
groups=("${_target_triplet}")
depends=('glibc>=2.20' 'zlib')

# Building information
options=('staticlibs' '!distcc' '!ccache')
source=("https://sourceware.org/pub/${_pkgname}/releases/${_pkgname}-${pkgver}.tar.xz")
md5sums=('5b7c9d4ce96f507d95c1b9a255e52418')


prepare() {
    cd "${srcdir}/${_pkgname}-${pkgver}"

    # hack! - libiberty configure tests for header files using "$CPP $CPPFLAGS"
    sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure

    mkdir "${srcdir}/${_pkgname}-build"
}


build() {
    cd "${srcdir}/${_pkgname}-build"

    # Specify --build, --host to avoid hangs when calling ldd.fakechroot.
    # See also https://github.com/dex4er/fakechroot/pull/59.
    "${srcdir}/${_pkgname}-${pkgver}/configure" \
        --prefix="${_toolroot}" \
        --build="${_local_triplet}" \
        --host="${_local_triplet}" \
        --target="${_target_triplet}" \
        --with-sysroot="/sysroot" \
        --disable-nls --disable-multilib \
        --disable-static --disable-werror \
        --disable-host-shared --disable-doc \
        --with-lib-path="${_toolroot}/lib"

    # Check the host environment and makes sure all the necessary tools are
    # available
    make configure-host

    make
}


package() {
    cd "${srcdir}/${_pkgname}-build"

    make prefix="${pkgdir}${_toolroot}" install

    # Remove the info files - I'm not using them and they conflict
    rm -rf "${pkgdir}${_toolroot}/share/info"
}
