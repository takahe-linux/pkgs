# Maintainer: Alastair Hughes < hobbitalastair at yandex dot com>

# Package information
pkgname='backhand'
pkgver='0.2'
pkgrel=14
pkgdesc="Utilities for managing background processes"

# Additional information...
arch=('mips' 'i586')
url="https://github.com/hobbitalastair/${pkgname}"
license=('MIT')

# Dependency information
groups=('base')
depends=('sh' 'hush') # Depend on hush for the service scripts.
makedepends=('musl')
hostdepends=('toolchain' 'git')

# Building information
source=("git+${url}#tag=${pkgver}")
md5sums=('SKIP')


build() {
    cd "${srcdir}/${pkgname}"
    make TIMEOUTCMD="timeout -s"
}


package() {
    cd "${srcdir}/${pkgname}"
    make DESTDIR="${pkgdir}/" install
    install -Dm0644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

    # Use hush instead of sh for the various scripts.
    for f in "${pkgdir}"/usr/bin/bh-{start,stop,stopall,status,release,require}; do
        sed -i "$f" -e 's:/usr/bin/sh:/usr/bin/hush:g'
    done

    # Create the directories for storing services and logs.
    mkdir -p "${pkgdir}/var/log/backhand/"
    mkdir -p "${pkgdir}/usr/lib/backhand/"
}
