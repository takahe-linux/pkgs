# Maintainer: Alastair Hughes < hobbitalastair at yandex dot com>

# Package information
pkgname='flex'
pkgver='2.6.0'
pkgrel=1
pkgdesc="A tool for generating text-scanning programs"

# Additional information...
arch=('mips' 'i586')
url="http://flex.sourceforge.net"
license=('custom')

# Dependency information
groups=('base-devel')
depends=('sh')
makedepends=('musl' 'bison' 'm4')
hostdepends=('toolchain')

# Building information
source=("http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.xz")
md5sums=('3cbbfa1554d0b75fad9f8100732454de')


build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    ./configure --prefix="/usr" \
        --host="${_target_triplet}" \
        --build="${_local_triplet}"
    # Disable tests; we can't run these while cross compiling.
    printf 'all:\n\ttrue\n\ninstall:\n\ttrue\n\n' > tests/Makefile.in
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    make DESTDIR="${pkgdir}/" install
    ln -s flex "${pkgdir}/usr/bin/lex"
    install -Dm644 COPYING \
        "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"

    # Remove the redundant charset.alias (provided by gettext).
    rm -f "${pkgdir}/usr/lib/charset.alias"
}
