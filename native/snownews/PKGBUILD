# Maintainer: Alastair Hughes < hobbitalastair at yandex dot com>

# Package information
pkgname='snownews'
pkgver='1.5.12'
pkgrel=18
pkgdesc="Text mode RSS newsreader"

# Additional information...
arch=('mips' 'i586')
url="http://kiza.kcore.de/software/snownews"
license=('GPL')

# Dependency information
makedepends=('libxml2' 'zlib')

# Building information
source=("http://kiza.kcore.de/software/snownews/download/${pkgname}-${pkgver}.tar.gz")
md5sums=('80da8943fc5aa96571924aec0087d4c0')


prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    # Use .config/snownews instead.
    for file in $(find ./); do
        if [ -f "${file}" ]; then
            sed -i "${file}" -e 's:\.snownews:.config/snownews:g'
        fi
    done
}


build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    # Configure is a perl script; write the file manually.

    cat > platform_settings << EOF
PREFIX= /usr
CFLAGS= ${CFLAGS} $(xml2-config --cflags) -DOS="\"linux\"" -DLOCALEPATH="\"\""
LDFLAGS= ${LDFLAGS} -lncurses -lcrypto -lz $(xml2-config --libs)
EOF

    make
}


package() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    make DESTDIR="${pkgdir}/" install
}
