# Maintainer: Alastair Hughes < hobbitalastair at yandex dot com>

#TODO: Split into libblkid, libuuid?

# Package information
pkgname=('e2fsprogs' 'e2fsprogs-libblkid' 'e2fsprogs-libuuid')
pkgver='1.42.12'
pkgrel=1

# Additional information...
arch=('i586')
url="http://e2fsprogs.sourceforge.net"
license=('GPL' 'LGPL' 'MIT')

# Dependency information
groups=('base')
depends=('libc')
makedepends=('bc')

# Building information
source=("http://downloads.sourceforge.net/sourceforge/${pkgname}/\
${pkgname}-${pkgver}.tar.gz"
        'MIT-LICENSE'
       )
sha1sums=('083c1bb0d1e85672e8038a2fadf70b24e7409db7'
          'f4a0d5b0cdb980e3fedd6f5e7dde0b0ffb7bbdfb')


prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"

    # Remove unnecessary init.d directory
    sed -i -e '/init\.d/s|^|#|' 'misc/Makefile.in'
}


build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

    ./configure CC="${CC}" HOSTCC="${CC}" \
                CFLAGS="${CFLAGS}" LDFLAGS="${LDFLAGS}" \
                --prefix="${_prefix}" \
                --libdir="${_libdir}" \
                --bindir="${_bindir}" \
                --sbindir="${_bindir}" \
                --build="${_local_triplet}" \
                --host="${_target_triplet}" \
                --without-ncurses --disable-nls --disable-fsck

	make CC="${CC}" HOSTCC="${HOSTCC}" \
        CFLAGS="${CFLAGS}" LDFLAGS="${LDFLAGS}" \
        HOSTCFLAGS="${HOSTCFLAGS}"
}


package_e2fsprogs() {
    pkgdesc='Ext2/3/4 filesystem utilities'
    depends+=('sh') # ?
    backup=('etc/mke2fs.conf')
    install="${pkgname}.install"

	cd "${srcdir}/${pkgbase}-${pkgver}"

	make DESTDIR="${pkgdir}/" install install-libs

    # Make sure awk is awk...
    sed -i -e 's/^AWK=.*/AWK=awk/' "${pkgdir}/usr/bin/compile_et"

    # Remove references to build directory
    sed -i -e 's#^SS_DIR=.*#SS_DIR="/usr/share/ss"#' \
        "${pkgdir}/usr/bin/mk_cmds"
    sed -i -e 's#^ET_DIR=.*#ET_DIR="/usr/share/et"#' \
        "${pkgdir}/usr/bin/compile_et"

    # Install MIT license
    install -Dm644 "${srcdir}/MIT-LICENSE" \
        "${pkgdir}/usr/share/licenses/${pkgname}/MIT-LICENSE"

    # Move etc to the right place...
    mv "${pkgdir}/usr/etc" "${pkgdir}/etc"

    # Remove library files
    rm "${pkgdir}/usr/lib/libblkid.a"
    rm -r "${pkgdir}/usr/include/blkid"
    rm "${pkgdir}/usr/share/man/man3/libblkid.3"
    rm "${pkgdir}/usr/lib/pkgconfig/blkid.pc"

    rm "${pkgdir}/usr/lib/libuuid.a"
    rm -r "${pkgdir}/usr/include/uuid"
    rm "${pkgdir}"/usr/share/man/man3/uuid*.3
    rm "${pkgdir}/usr/lib/pkgconfig/uuid.pc"
}


package_e2fsprogs-libblkid() {
    pkgdesc='Blkid library, from e2fsprogs'
    provides=('llibblkid')
    conflicts=('libblkid')

	cd "${srcdir}/${pkgbase}-${pkgver}"

	make DESTDIR="${pkgdir}/" install-libs

    # Install MIT license
    install -Dm644 "${srcdir}/MIT-LICENSE" \
        "${pkgdir}/usr/share/licenses/${pkgname}/MIT-LICENSE"

    # Clean up build directory... remove unneeded files
    rm -r "${pkgdir}/usr/share/man/man1/"
    rm -r "${pkgdir}/usr/share/man/man3/"{uuid*,com_err.3}
    rm -r "${pkgdir}/usr/share/"{ss,et}
    rm -r "${pkgdir}/usr/bin"
    rm -r "${pkgdir}/usr/lib/pkgconfig/"{com_err.pc,e2p.pc,ext2fs.pc,ss.pc,\
uuid.pc}
    rm -r "${pkgdir}/usr/lib/"{libcom_err.a,libe2p.a,libext2fs.a,\
libss.a,libuuid.a}
    rm -r "${pkgdir}/usr/include/"{com_err.h,e2p,et,ext2fs,ss,uuid}
}

package_e2fsprogs-libuuid() {
    pkgdesc='UUID library, from e2fsprogs'
    provides=('libuuid')
    conflicts=('libuuid')

	cd "${srcdir}/${pkgbase}-${pkgver}"

	make DESTDIR="${pkgdir}/" install-libs

    # Install MIT license
    install -Dm644 "${srcdir}/MIT-LICENSE" \
        "${pkgdir}/usr/share/licenses/${pkgname}/MIT-LICENSE"

    # Clean up build directory... remove unneeded files
    rm -r "${pkgdir}/usr/share/man/man1/"
    rm -r "${pkgdir}/usr/share/man/man3/"{libblkid.3,com_err.3}
    rm -r "${pkgdir}/usr/share/"{ss,et}
    rm -r "${pkgdir}/usr/bin"
    rm -r "${pkgdir}/usr/lib/pkgconfig/"{com_err.pc,e2p.pc,ext2fs.pc,ss.pc,\
blkid.pc}
    rm -r "${pkgdir}/usr/lib/"{libcom_err.a,libe2p.a,libext2fs.a,\
libss.a,libblkid.a}
    rm -r "${pkgdir}/usr/include/"{com_err.h,e2p,et,ext2fs,ss,blkid}
}


