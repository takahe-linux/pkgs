# Maintainer: Alastair Hughes < hobbitalastair at yandex dot com>

# Package information
pkgname='curl'
pkgver='7.49.1'
pkgrel=1
pkgdesc="An URL retrieval utility and library"

# Additional information...
arch=('mips' 'i586')
url="http://curl.haxx.se"
license=('MIT')

# Dependency information
groups=('base')
makedepends=('openssl' 'zlib' 'musl')
hostdepends=('toolchain')

# Building information
source=("http://curl.haxx.se/download/${pkgname}-${pkgver}.tar.gz")
md5sums=('2feb3767b958add6a177c6602ff21e8c')


build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    ./configure \
        --prefix="/usr" \
        --build="${_local_triplet}" \
        --host="${_target_triplet}" \
        --target="${_target_triplet}" \
        --mandir="/usr/share/man" \
        --disable-ldap \
        --without-libidn \
        --with-ssl \
        --disable-shared \
        --enable-static
    make
}


package() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    make DESTDIR="${pkgdir}/" install
    install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
