# Maintainer: Alastair Hughes < hobbitalastair at yandex dot com>

# Based off the Arch Linux PKGBUILD

# Package information
pkgname='pacman'
pkgver='5.0.1'
pkgrel=2
pkgdesc="Arch Linux's simple package manager"

# Additional information...
arch=('i586' 'mips' 'x86_64')
url="http://www.archlinux.org/pacman/"
license=('GPL')

# Dependency information
groups=('base' 'base-devel')
depends=('bash' 'libarchive' 'gettext' 'openssl')
makedepends=('zlib' 'musl')
hostdepends=('asciidoc' 'libtool' 'toolchain')
optdepends=('wget')

# Building information
backup=('etc/pacman.conf' 'etc/makepkg.conf')
source=("https://sources.archlinux.org/other/pacman/${pkgname}-${pkgver}.tar.gz"
        pacman.conf
        makepkg.conf)
md5sums=('377a2664d6007d72d6d8a126add83bcf'
         '6b40bff8bd90882a7d3f6b3024f74d38'
         '99acb0e7ec4c35f7d01bdb62447957bb')


prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    # Remove AC_FUNC_MALLOC - this is autodetected at runtime so causes a
    # build failure!
    sed -i -e '/AC_FUNC_MALLOC/d' 'configure.ac'
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    ./configure --prefix="/usr" \
        --host="${_target_triplet}" \
        --bindir="/usr/bin" \
        --localstatedir='/var' \
        --sysconfdir="/etc" \
        --enable-doc \
        --with-scriptlet-shell="/usr/bin/bash" \
        --without-gpgme \
        --without-openssl \
        --without-curl \
        --without-libcurl \
        --disable-shared

    make LIBS="-lz"
    make -C contrib LIBS="-lz"
}


package() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    make DESTDIR="${pkgdir}/" install
    make DESTDIR="${pkgdir}/" -C contrib install

    # Install the configuration files
    install -dm755 "${pkgdir}/etc"
    install -m644 "${srcdir}/pacman.conf" "${pkgdir}/etc/pacman.conf"

    # Install and update the makepkg config file
    install -m644 "${srcdir}/makepkg.conf" "${pkgdir}/etc/makepkg.conf"
    sed -i "${pkgdir}/etc/makepkg.conf" \
        -e "s|@CARCH[@]|${_target_arch}|g" \
        -e "s|@CHOST[@]|${_target_triplet}|g" \
        -e "s|@CARCHFLAGS[@]|${_target_cflags}|g"

    # Install the bash completion files
    install -dm755 "${pkgdir}/usr/share/bash-completion/completions"
    mv "${pkgdir}/etc/bash_completion.d/pacman" \
        "${pkgdir}/usr/share/bash-completion/completions"
    rmdir "${pkgdir}/etc/bash_completion.d"

    for file in makepkg pacman-key; do
        ln -s 'pacman' "${pkgdir}/usr/share/bash-completion/completions/${file}"
    done

    install -Dm644 'contrib/PKGBUILD.vim' \
        "${pkgdir}/usr/share/vim/vimfiles/syntax/PKGBUILD.vim"
}
