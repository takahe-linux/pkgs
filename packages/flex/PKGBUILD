# Maintainer: Alastair Hughes < hobbitalastair at yandex dot com>

# Package information
pkgname='flex'
pkgver='2.6.1'
pkgrel=1
pkgdesc="A tool for generating text-scanning programs"

# Additional information...
arch=('mips' 'i586')
url="https://github.com/westes/flex"
license=('custom')

# Dependency information
groups=('base-devel')
depends=('sh')
makedepends=('musl' 'bison' 'm4')
hostdepends=('toolchain')

# Building information
source=("${url}/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.xz"
        "stage1flex.patch" # https://github.com/westes/flex/issues/78
        )
md5sums=('cd3c86290fc2676a641aefafeb10848a'
         '915321d31ef144e0c01886c3cf564c36')


build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    ./configure --prefix="/usr" \
        --host="${_target_triplet}" \
        --build="${_local_triplet}" \
        --disable-shared
    # Disable tests; we can't run these while cross compiling.
    printf 'all:\n\ttrue\n\ninstall:\n\ttrue\n\n' > tests/Makefile.in

    # Patch the makefile.
    patch src/Makefile < "${srcdir}/stage1flex.patch"

    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    make DESTDIR="${pkgdir}/" install
    ln -s flex "${pkgdir}/usr/bin/lex"
    install -Dm644 COPYING \
        "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"

    # Remove the redundant charset.alias (provided by gettext).
    rm -f "${pkgdir}/usr/lib/charset.alias"
    rm -rf "${pkgdir}/usr/share/info"
}
