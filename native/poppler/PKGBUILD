# Maintainer: Alastair Hughes < hobbitalastair at yandex dot com>

# Package information
pkgname='poppler'
pkgver='0.59.0'
pkgrel=2
pkgdesc="PDF rendering library and toolkit"

# Additional information...
arch=('mips' 'i586')
url="https://poppler.freedesktop.org/"
license=('GPL')

# Dependency information
makedepends=('libpng' 'libjpeg' 'fontconfig' 'freetype' 'expat' 'zlib')

# Building information
source=("${url}/${pkgname}-${pkgver}.tar.xz")
md5sums=('6e44408a3b4f4a738f8a6770d0aea8a5')


build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    # Use a static PKG_CONFIG.
    export PKG_CONFIG="pkg-config --static"

    ./configure --prefix="/usr" \
        --enable-zlib \
        --enable-dctdecoder=libjpeg \
        --enable-libopenjpeg=none \
        --enable-libpng \
        --disable-shared
    make
}


check() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make check
}


package() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    make DESTDIR="${pkgdir}/" install
}
