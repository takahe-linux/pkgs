# Maintainer: Alastair Hughes < hobbitalastair at yandex dot com>

# Package information
pkgname='libpng'
pkgver='1.6.29'
pkgrel=1
pkgdesc="A collection of PNG manipulation routines"

# Additional information...
arch=('mips' 'i586')
url="http://www.libpng.org/pub/png/libpng.html"
license=('custom')

# Dependency information
makedepends=('zlib')

# Building information
source=("http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.xz")
md5sums=('3245dbd76ea91e1437507357b858ec97')


build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    ./configure --prefix="/usr" \
        --disable-shared \
        --enable-static
    make
}


check() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    make check
}


package() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    make DESTDIR="${pkgdir}/" install
    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
