# Maintainer: Alastair Hughes < hobbitalastair at yandex dot com>

# Package information
pkgname='strace'
pkgver='4.14'
pkgrel=18
pkgdesc="syscall tracer"

# Additional information...
arch=('mips' 'i586')
url="http://sourceforge.net/projects/strace/"
license=('custom')

# Dependency information
makedepends=('linux-api-headers')

# Building information
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.xz")
md5sums=('1e39b5f7583256d7dc21170b9da529ae')


prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    # Use the kernel's sgidefs.h on mips.
    sed -e 's:<sgidefs.h>:<asm/sgidefs.h>:' -i configure
}


build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    ./configure --prefix="/usr"
    make
}


package() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    make DESTDIR="${pkgdir}/" install
    install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
