# Maintainer: Alastair Hughes < hobbitalastair at yandex dot com>

# Package information
pkgname='attr'
pkgver='2.4.47'
pkgrel=3
pkgdesc="Extended attribute support library"

# Additional information...
arch=('mips' 'i586')
url="http://savannah.nongnu.org/projects/attr"
license=('LGPL')

# Dependency information
makedepends=('gettext')

# Building information
source=("https://download.savannah.gnu.org/releases/${pkgname}/${pkgname}-${pkgver}.src.tar.gz"
        "attr-cdefs.patch")
md5sums=('84f58dec00b60f2dc8fd1c9709291cc7'
         'a7b4b287878ec5b717d33ce8e45ca389')


prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    patch -p1 < "${srcdir}/attr-cdefs.patch"
}


build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    export INSTALL_USER=root INSTALL_GROUP=root
    ./configure --prefix="/usr" --libdir="/usr/lib" --libexecdir="/usr/lib"
    make
}


package() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    make DIST_ROOT="${pkgdir}/" install install-lib install-dev
    chmod 0755 "${pkgdir}"/usr/lib/libattr.so.*.*.*
}
