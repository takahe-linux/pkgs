# Maintainer: Alastair Hughes < hobbitalastair at yandex dot com>

# Package information
_pkgname='gcc'
pkgname="${_target_triplet}-${_pkgname}"
pkgver='6.3.0'
pkgrel=5
pkgdesc="The GNU Compiler Collection"

# Additional information...
arch=('x86_64')
url='http://gcc.gnu.org/'
license=('GPL' 'LGPL' 'FDL')

# Dependency information
groups=("${_target_triplet}")
depends=("${_target_triplet}-binutils" 'zlib' 'libmpc' 'gmp' 'mpfr')
makedepends=("${_target_triplet}-gcc-static")
targetdepends=("musl" "linux-api-headers")
conflicts=("${_target_triplet}-gcc-static")

# Building information
options=(!emptydirs !strip)
source=("ftp://gcc.gnu.org/pub/gcc/releases/gcc-${pkgver}/gcc-${pkgver}.tar.bz2"
        *.patch)
sha256sums=('f06ae7f3f790fbf0f018f6d40e844451e6bc3b7bc96e128e63b09825c1f8b29f'
            'fe3334cffd41d3becfe3e57bea38822678b463a0947a3141195d953aab8d26e8'
            '7ad65c8a8613b34b7ffdf0dcee15659ba12a21ed0b65052644d6ca73653d337a'
            '9854b80d6b4854f48438f6784e86411fbc607763a6be80faae5a36c508c3cf9e'
            'e0f0c2d556ab5e0367f579307db1b1b5abfc4533ccf423597eabcefae52430af'
            'c00abb172cd9aee0c951450b4e77d320ea6fd76a2901e96547113c3d0e9bf210')


prepare() {
    cd "${srcdir}/${_pkgname}-${pkgver}"

    echo "${pkgver}" > gcc/BASE-VER

    # hack! - some configure tests for header files using "$CPP $CPPFLAGS"
    sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" {libiberty,gcc}/configure

    # Disable fixincludes (could lead to header pollution)
    sed -i 's@\./fixinc\.sh@-c true@' gcc/Makefile.in

    # Apply the patches...
    for p in "${srcdir}"/*.patch; do
        patch -p1 < "${p}"
    done
    
    mkdir "${srcdir}/gcc-build"
}


build() {
    cd "${srcdir}/gcc-build"

    # --enable-cxx-flags is a hack, because currently libstdc++ uses CXXFLAGS,
    # not CXXFLAGS_FOR_TARGET. See GCC bug 53579...
    # Sigh...
    # TODO: Figure out exactly how the build systems _works_ (or doesn't) so
    # that I can patch it instead of applying this hack...
    # WARNING: To use -Os or -O2, you need more than 2GB of space (tmp if you
    # are using /tmp for compiling).
    export CXXFLAGS="-Os"

    # Also, disable libsanitizer, libatomic, libmudflap, libmpx, and mudflap
    # (they do not build).
    LDFLAGS="-Wl,-rpath,${_toolroot}/lib" \
    "${srcdir}/${_pkgname}-${pkgver}/configure" \
        --build="${_local_triplet}" \
        --host="${_local_triplet}" \
        --target="${_target_triplet}" \
        --prefix="${_toolroot}" \
        --with-sysroot="/sysroot" \
        --with-native-system-header-dir="/usr/include" \
        --with-local-prefix="/sysroot" \
        --libexecdir="/usr/lib" \
        --enable-languages=c,c++ \
        --enable-c99 \
        --enable-__cxa_atexit \
        --enable-threads=posix \
        --enable-libstdcxx-time \
        --enable-cxx-flags="${CXXFLAGS_FOR_TARGET}" \
        --disable-nls \
        --disable-shared \
        --disable-multilib \
        --disable-libmudflap \
        --disable-mudflap \
        --disable-libatomic \
        --disable-libsanitizer \
        --disable-libmpx \
        --disable-libcc1 \
        --with-gnu-as \
        --with-gnu-ld \
        --with-system-zlib

    make
}


package() {
    cd "${srcdir}/gcc-build"

    make DESTDIR="${pkgdir}" install -j1

    # strip target binaries
    find "${pkgdir}/usr/lib/gcc/${_target_triplet}/" \
         "${pkgdir}${_toolroot}/lib" \
         -type f -and \( -name \*.a -or -name \*.o \) \
         -exec "${_toolroot}/bin/${_target_triplet}-objcopy" \
         -R .comment -R .note -R .debug_info -R .debug_aranges \
         -R .debug_pubnames -R .debug_pubtypes -R .debug_abbrev -R .debug_line \
         -R .debug_str -R .debug_ranges -R .debug_loc '{}' \;

    # strip host binaries
    find "${pkgdir}${_toolroot}/bin/" \
         "${pkgdir}/usr/lib/gcc/${_target_triplet}/" \
         -type f -and \( -executable \) -exec strip '{}' \;

    # Remove info files (already existing)
    rm -r "${pkgdir}${_toolroot}/share/info"
}
