# Maintainer: Alastair Hughes < hobbitalastair at yandex dot com>

# Package information
pkgname='libcss'
pkgver='0.7.0'
pkgrel=20
pkgdesc="CSS library from the Netsurf project"

# Additional information...
arch=('mips' 'i586')
url="http://www.netsurf-browser.org/"
license=('MIT' 'GPL2')

# Dependency information
depends=('libwapcaplet' 'libparserutils')
makedepends=('git' 'netsurf-buildsystem' 'perl')

# Building information
source=("git://git.netsurf-browser.org/${pkgname}.git#tag=release/${pkgver}")
md5sums=('SKIP')


prepare() {
    cd "${srcdir}/${pkgname}"
    sed -i Makefile -e 's:-Werror::g' # Don't build with Werror.
}


build() {
    cd "${srcdir}/${pkgname}"
    make LIBDIR=lib INCLUDEDIR=include PREFIX=/usr COMPONENT_TYPE="lib-shared"
}


package() {
    cd "${srcdir}/${pkgname}"

    make LIBDIR=lib INCLUDEDIR=include PREFIX=/usr COMPONENT_TYPE="lib-shared" \
        DESTDIR="${pkgdir}/" install
    install -Dm644 COPYING \
        "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
