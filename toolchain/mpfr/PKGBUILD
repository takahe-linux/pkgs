# Maintainer: Alastair Hughes < hobbitalastair at yandex dot com>

# Based off the Arch Linux pkgbuild

# Package information
_pkgname='mpfr'
pkgname="${_target_triplet}-${_pkgname}"
_pkgver='3.1.2'
_patchlevel='p11'
pkgver="${_pkgver}.${_patchlevel}"
pkgrel=1
pkgdesc="Multiple-precision floating-point library"

# Additional information...
arch=('x86_64')
url="http://www.mpfr.org/"
license=('LGPL')

# Dependency information
groups=("${_target_triplet}")
depends=("${_target_triplet}-gmp>=5.0")

# Building information
source=("http://www.mpfr.org/mpfr-current/${_pkgname}-${_pkgver}.tar.xz"
        "${_pkgname}-${_pkgver}-${_patchlevel}.patch")
md5sums=('e3d203d188b8fe60bb6578dd3152e05c'
         '9f96a5c7cac1d6cd983ed9cf7d997074')


prepare() {
    cd "${srcdir}/${_pkgname}-${_pkgver}"

    patch -p1 -i "${srcdir}/${_pkgname}-${_pkgver}-${_patchlevel}.patch"
}


build() {
    cd "${srcdir}/${_pkgname}-${_pkgver}"

    LDFLAGS="-Wl,-rpath,${_toolroot}/lib" \
    ./configure --prefix="${_toolroot}" --enable-thread-safe --enable-shared \
        --disable-static --with-gmp="${_toolroot}"
    make
}


check() {
    cd "${srcdir}/${_pkgname}-${_pkgver}"

    make check
}


package() {
    cd "${srcdir}/${_pkgname}-${_pkgver}"

    make DESTDIR="${pkgdir}/" install

    rm -rf "${pkgdir}${_toolroot}/share/info"
}
