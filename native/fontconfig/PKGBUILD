# Maintainer: Alastair Hughes < hobbitalastair at yandex dot com>

# Package information
pkgname='fontconfig'
pkgver='2.12.1'
pkgrel=21
pkgdesc="A library for customising font access"

# Additional information...
arch=('mips' 'i586')
url="https://www.fontconfig.org/release"
license=('custom')

# Dependency information
makedepends=('expat' 'freetype' 'pkg-config' 'zlib' 'libpng')

# Building information
source=("${url}/${pkgname}-${pkgver}.tar.bz2")
md5sums=('b5af5a423ee3b5cfc34846838963c058')


build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    # Use a static PKG_CONFIG.
    export PKG_CONFIG="pkg-config --static"

    ./configure --prefix="/usr" \
        --sysconfdir="/etc" \
        --with-templatedir="/etc/fonts/conf.d" \
        --with-xmldir="/etc/fonts" \
        --localstatedir="/var" \
        --disable-shared \
        --with-default-fonts="/usr/share/fonts" \
        --without-add-fonts
    make V=1
}


check() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make -k check
}


package() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    make DESTDIR="${pkgdir}/" install
    install -Dm0644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
