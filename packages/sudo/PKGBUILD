# Maintainer: Alastair Hughes < hobbitalastair at yandex dot com>

# Package information
pkgname='sudo'
_sudover='1.8.15'
pkgver="${_sudover/p/.p}"
pkgrel=1
pkgdesc="Give certain users the ability to run some commands as root"

# Additional information...
arch=('i586' 'mips')
url="http://www.sudo.ws/sudo/"
license=('custom')

# Dependency information
groups=('base-devel')
# Depends: packages/musl targets/toolchain
depends=('libc')

# Building information
source=("http://www.sudo.ws/sudo/dist/${pkgname}-${_sudover}.tar.gz")
md5sums=('7cf6b9b76d0478a572432bed481dd7b5')


build() {
    cd "${srcdir}/${pkgname}-${_sudover}"

    ./configure \
        --host="${_target_triplet}" \
        --build="${_local_triplet}" \
        --prefix="${_prefix}" \
        --bindir="${_bindir}" \
        --sbindir="${_bindir}" \
        --libdir="${_libdir}" \
        --libexecdir="${_libdir}" \
        --with-rundir="/run/sudo" \
        --with-vardir="/var/db/sudo" \
        --with-logfac=auth \
        --with-env-editor \
        --disable-shared \
        --disable-shared-libutil \
        --disable-tmpfiles.d \
        --enable-static-sudoers \
        --with-passprompt="[sudo] password for %p: "

    # We build mksigname/mksiglist first, to work around sudo not cross
    # compiling nicely (from openembedded patch 45851).
    make CFLAGS="${HOSTCFLAGS}" CC="${HOSTCC}" \
        CPPFLAGS="${CPPFLAGS} -I../../include -I../../ -I../" \
        -C lib/util/ mksigname mksiglist

    make
}


check() {
    cd "${srcdir}/${pkgname}-${_sudover}"

    make check
}


package() {
    cd "${srcdir}/${pkgname}-${_sudover}"

    make DESTDIR="${pkgdir}/" install

    install -Dm644 doc/LICENSE "${pkgdir}${_licensedir}/sudo/LICENSE"
}
