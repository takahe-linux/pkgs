# Maintainer: Alastair Hughes < hobbitalastair at yandex dot com>

# Package information
pkgname='wpa_supplicant'
pkgver='2.6'
pkgrel=52
pkgdesc="Utility for key negotiation for WPA networks"

# Additional information...
arch=('mips' 'i586')
url="http://hostap.epitest.fi/wpa_supplicant"
license=('GPL')

# Dependency information
depends=('libnl' 'zlib' 'readline' 'openssl')

# Building information
source=("http://w1.fi/releases/${pkgname}-${pkgver}.tar.gz"
        "wpa_supplicant.run")
md5sums=('091569eb4440b7d7f2b4276dbfc03c3c'
         '3396a38f6675d3fbb8327b05d2ca882e')

prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}/${pkgname}"
    
    # Generate the config file.
    cat > .config << EOF
# Enable the NL80211 driver.
CONFIG_DRIVER_NL80211=y
CONFIG_LIBNL32=y

# Enable some selected EAP options.
CONFIG_EAP_PEAP=y
CONFIG_EAP_TTLS=y

# Enable readline.
CONFIG_READLINE=y
# Alternative:
CONFIG_WPA_CLI_EDIT=y

# Enable the control interface.
CONFIG_CTRL_IFACE=y

# Disable debug output; reduces binary size.
CONFIG_NO_STDOUT_DEBUG=y

# Set the config backend.
CONFIG_BACKEND=file
CONFIG_NO_CONFIG_BLOBS=y

# We use openssl.
CONFIG_TLS=openssl

# Add the flags.
CFLAGS += ${CFLAGS}
LDFLAGS += ${LDFLAGS}
# Set the default paths.
LIBDIR = /usr/lib
INCDIR = /usr/include
BINDIR = /usr/bin
# Set the cc.
CC = gcc
EOF

    # We need to link zlib whenever we link against ssl or crypto.
    sed -i 'Makefile' -e 's:-lssl:-lssl -lz:g' \
        -e 's:-lcrypto:-lcrypto -lz:g'
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}/${pkgname}"
    make
}


package() {
    cd "${srcdir}/${pkgname}-${pkgver}/${pkgname}"
    
    make DESTDIR="${pkgdir}/" install

    # Install/clean the man pages.
    for i in 5 8; do
        install -dm0755 "${pkgdir}/usr/share/man/man${i}"
        install -m0644 doc/docbook/*."${i}" \
            "${pkgdir}/usr/share/man/man${i}"
    done
    rm -f "${pkgdir}/usr/share/man/man8/wpa_"{priv,gui}.8

    # Add a custom service for use with backhand.
    install -Dm755 "${srcdir}/wpa_supplicant.run" \
        "${pkgdir}/usr/lib/backhand/wpa_supplicant/run"
}
