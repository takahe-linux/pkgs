# Maintainer: Alastair Hughes < hobbitalastair at yandex dot com>

# Package information
pkgname='e2fsprogs'
pkgver='1.43.9'
pkgrel=1
pkgdesc='Ext2/3/4 filesystem utilities'

# Additional information...
arch=('mips' 'i586')
url="https://github.com/tytso/e2fsprogs"
license=('GPL' 'LGPL' 'MIT')

# Building information
backup=('etc/mke2fs.conf')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
sha1sums=('6adac37d468e55cd9ae68adc231019e2fe1d3727')


build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    ./configure --prefix="/usr" \
        --sbindir="/usr/bin" \
        --sysconfdir="/etc" \
        --enable-elf-shlibs \
        --disable-fsck
    make
}


package() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    make DESTDIR="${pkgdir}/" install install-libs
    install -Dm644 "NOTICE" "${pkgdir}/usr/share/licenses/${pkgname}/NOTICE"

    # Make sure awk is awk.
    sed -i -e 's/^AWK=.*/AWK=awk/' "${pkgdir}/usr/bin/compile_et"

    # Remove redundant files.
    rm -rf "${pkgdir}/usr/share/info"
    rm -f "${pkgdir}/usr/lib/charset.alias"
}
