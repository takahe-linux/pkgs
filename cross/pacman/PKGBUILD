# Maintainer: Alastair Hughes < hobbitalastair at yandex dot com>

# Based off the Arch Linux PKGBUILD

# Package information
pkgname='pacman'
pkgver='5.0.2'
pkgrel=19
pkgdesc="Arch Linux's simple package manager"

# Additional information...
arch=('i586' 'mips' 'x86_64')
url="http://www.archlinux.org/pacman/"
license=('GPL')

# Dependency information
groups=('base' 'base-devel')
depends=('bash' 'curl' 'libarchive' 'gettext' 'xz' 'musl' 'openssl')
hostdepends=('asciidoc' 'libtool' 'toolchain')
optdepends=('wget')

# Building information
backup=('etc/pacman.conf' 'etc/makepkg.conf')
source=("https://sources.archlinux.org/other/pacman/${pkgname}-${pkgver}.tar.gz"
        pacman.conf
        makepkg.conf
        *.patch)
md5sums=('f36f5e7e95a89436febe1bcca874fc33'
         '044786f82c98ca027067c63037e1fa15'
         'fefa873b52d15f08a1d32cb469ec6018'
         '16fe9fbb5bfeaaefdffcfe7cab147445'
         '8814d0b46687cc017d44d4dd5eebd1c1')


prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    autoreconf --force --install

    # Upstreamed, remove with next release.
    cat > config.cache << EOF
ac_cv_func_malloc_0_nonnull=yes
EOF

    for p in ../*.patch; do
        patch -p1 < "${p}"
    done
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    ./configure --prefix="/usr" \
        --host="${_target_triplet}" \
        --bindir="/usr/bin" \
        --localstatedir='/var' \
        --sysconfdir="/etc" \
        --enable-doc \
        --cache-file=config.cache \
        --with-scriptlet-shell="/usr/bin/bash" \
        --without-gpgme \
        DUFLAGS="-sk" \
        SEDINPLACEFLAGS="-i"

    make
    make -C contrib
}


package() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    make DESTDIR="${pkgdir}/" install
    make DESTDIR="${pkgdir}/" -C contrib install

    # Install the configuration files
    install -dm755 "${pkgdir}/etc"
    install -m644 "${srcdir}/pacman.conf" "${pkgdir}/etc/pacman.conf"

    # Install and update the makepkg config file
    install -m644 "${srcdir}/makepkg.conf" "${pkgdir}/etc/makepkg.conf"
    sed -i "${pkgdir}/etc/makepkg.conf" \
        -e "s|@CARCH[@]|${_target_arch}|g" \
        -e "s|@CHOST[@]|${_target_triplet}|g" \
        -e "s|@CFLAGS[@]|${_target_cflags}|g" \
        -e "s|@CPPFLAGS[@]|${_target_cppflags}|g" \
        -e "s|@LDFLAGS[@]|-Wl${_target_ldflags}|g"

    # Install the bash completion files
    install -dm755 "${pkgdir}/usr/share/bash-completion/completions"
    mv "${pkgdir}/etc/bash_completion.d/pacman" \
        "${pkgdir}/usr/share/bash-completion/completions"
    rmdir "${pkgdir}/etc/bash_completion.d"

    for file in makepkg pacman-key; do
        ln -s 'pacman' "${pkgdir}/usr/share/bash-completion/completions/${file}"
    done

    install -Dm644 'contrib/PKGBUILD.vim' \
        "${pkgdir}/usr/share/vim/vimfiles/syntax/PKGBUILD.vim"
}
