# Maintainer: Alastair Hughes < hobbitalastair at yandex dot com>

# Package information
pkgname='flex'
pkgver='2.6.2'
pkgrel=2
pkgdesc="A tool for generating text-scanning programs"

# Additional information...
arch=('mips' 'i586')
url="https://github.com/westes/flex"
license=('custom')

# Dependency information
groups=('base-devel')
depends=('sh')
makedepends=('musl' 'bison' 'm4')
hostdepends=('toolchain' "${_target_triplet}-flex")

# Building information
source=("${url}/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.gz"
        "stage1flex.patch" # https://github.com/westes/flex/issues/78
        "keep-man.patch" # https://github.com/westes/flex/issues/108
        )
md5sums=('cc6d76c333db7653d5caf423a3335239'
         '759bb084df57d871261ea0359625b7f9'
         '9b01769868001b84b1c99a9869e50116')


prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    patch -p1 < "${srcdir}/stage1flex.patch"
    patch -p1 < "${srcdir}/keep-man.patch"
    ./autogen.sh
    touch "doc/flex.1" # Use the provided man page.
}


build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    ./configure --prefix="/usr" \
        --host="${_target_triplet}" \
        --build="${_local_triplet}" \
        --disable-shared
    # TODO: Disable tests (can't run while cross compiling, see issue #49)
    printf 'all:\n\ttrue\n\ninstall:\n\ttrue\n\n' > tests/Makefile.in

    make
}


package() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    make DESTDIR="${pkgdir}/" install
    ln -s flex "${pkgdir}/usr/bin/lex"
    install -Dm644 COPYING \
        "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"

    # Remove the redundant charset.alias (provided by gettext).
    rm -f "${pkgdir}/usr/lib/charset.alias"
    rm -rf "${pkgdir}/usr/share/info"
}
