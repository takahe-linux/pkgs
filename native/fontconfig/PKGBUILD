# Maintainer: Alastair Hughes < hobbitalastair at yandex dot com>

# Package information
pkgname='fontconfig'
pkgver='2.12.6'
pkgrel=5
pkgdesc="A library for customising font access"

# Additional information...
arch=('mips' 'i586')
url="https://www.fontconfig.org/release"
license=('custom')

# Dependency information
depends=('expat' 'freetype' 'zlib' 'libpng')
makedepends=('gperf')

# Building information
source=("${url}/${pkgname}-${pkgver}.tar.bz2")
md5sums=('733f5e2371ca77b69707bd7b30cc2163')


build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    ./configure --prefix="/usr" \
        --sysconfdir="/etc" \
        --with-templatedir="/etc/fonts/conf.d" \
        --with-xmldir="/etc/fonts" \
        --localstatedir="/var" \
        --with-default-fonts="/usr/share/fonts" \
        --without-add-fonts
    make
}


check() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make -k check
}


package() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    make DESTDIR="${pkgdir}/" install
    install -Dm0644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
