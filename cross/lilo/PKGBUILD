# Maintainer: Alastair Hughes < hobbitalastair at yandex dot com>

# Package information
pkgname='lilo'
pkgver='24.2'
pkgrel=5
pkgdesc="Simple i386 linux bootloader"

# Additional information...
arch=('i586')
url="http://lilo.alioth.debian.org/"
license=('custom:BSD')

# Dependency information
makedepends=('musl' 'linux-api-headers')
hostdepends=('toolchain' 'bin86' 'sharutils')

# Building information
backup=('etc/lilo.conf')
source=("https://lilo.alioth.debian.org/ftp/sources/${pkgname}-${pkgver}.tar.gz")
md5sums=('fe5e8c9754cee342b958b5fcbbb6eb51')


prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    # To configure, edit make.vars and lilo.defines.
    cat > lilo.defines << EOF
-DDSECS=3 -DOLD_DIRSTR -DVERSION -DREADONLY
EOF
    cat >> make.vars << EOF
SBIN_DIR=/usr/bin
USRSBIN_DIR=/usr/bin
EOF

    # We work around glibc dependency...
    sed -e 's:__GLIBC[^ \t]*__:22:g' -i src/*.c
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make CC="${CROSS_COMPILE}gcc"
}


package() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    make DESTDIR="${pkgdir}/" USRSBIN_DIR="/usr/bin" install

    install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
    mv "${pkgdir}/etc/lilo.conf_example" "${pkgdir}/etc/lilo.conf"
    # Remove crud.
    rm -rf "${pkgdir}/boot"
    rm -rf "${pkgdir}/etc/"{initramfs,kernel}
}
