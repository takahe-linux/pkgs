# Maintainer: Alastair Hughes < hobbitalastair at yandex dot com>

# Package information
_pkgname='binutils'
pkgname="${_target_triplet}-${_pkgname}"
pkgver='2.28'
pkgrel=1
pkgdesc="A set of programs to assemble and manipulate binary and object files"

# Additional information...
arch=('x86_64')
url="http://www.gnu.org/software/${_pkgname}/"
license=('GPL')

# Dependency information
# Depends: packages/linux-api-headers
groups=("${_target_triplet}")
depends=('glibc>=2.20' 'zlib')

# Building information
options=('staticlibs' '!distcc' '!ccache')
source=("ftp://ftp.gnu.org/gnu/${_pkgname}/${_pkgname}-${pkgver}.tar.bz2"
        *.patch)
md5sums=('9e8340c96626b469a603c15c9d843727'
         'ee3f89bd29dd6c70fab7bde3640f5f1f')


prepare() {
    cd "${srcdir}/${_pkgname}-${pkgver}"

    # hack! - libiberty configure tests for header files using "$CPP $CPPFLAGS"
    sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure

    mkdir "${srcdir}/${_pkgname}-build"

    for patch in "${srcdir}/"*.patch; do
        patch -p1 < "${patch}"
    done
}


build() {
    cd "${srcdir}/${_pkgname}-build"

    "${srcdir}/${_pkgname}-${pkgver}/configure" \
        --prefix="${_toolroot}" \
        --target="${_target_triplet}" \
        --with-sysroot="/sysroot" \
        --disable-nls --disable-multilib \
        --disable-static --disable-werror \
        --disable-host-shared --disable-doc \
        --with-lib-path="${_toolroot}/lib"

    # Check the host environment and makes sure all the necessary tools are
    # available
    make configure-host

    make
}


package() {
    cd "${srcdir}/${_pkgname}-build"

    make prefix="${pkgdir}${_toolroot}" install

    # Remove file conflicting with host binutils and manpages for MS Windows tools
    rm "${pkgdir}${_toolroot}/share/man/man1/${_target_triplet}-"{dlltool,nlmconv,windres,windmc}*

    # Remove the info files - I'm not using them and they conflict
    rm -rf "${pkgdir}${_toolroot}/share/info"
}
