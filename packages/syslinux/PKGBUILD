# Maintainer: Alastair Hughes < hobbitalastair at yandex dot com>

# Based of the Arch Linux PKGBUILD

# This PKGBUILD does _not_ support EFI booting...

#TODO: Figure out how to strip off more cruft (network boot...)

# Package information
pkgname='syslinux'
pkgver='6.03'
pkgrel=1
pkgdesc='Collection of boot loaders that boot off a variety of mediums'

# Additional information...
arch=('i586') # Syslinux is i386/x86_64 only...
url="http://www.syslinux.org"
license=('GPL2')

# Dependency information
# depends=('bash') # Bash is required for syslinux_install-update script
makedepends=('git' 'python2' 'nasm' # For building... needs python?
             'upx' # ???
             'asciidoc' # ????
             'libuuid' # For uuid.h!
            )
optdepends=('perl-passwd-md5:  For md5pass'
            'perl-digest-sha1: For sha1pass'
            'mtools:           For mkdiskimage support'
            'util-linux:       For isohybrid'
           )
provides=('bootloader')
conflicts=('bootloader')

# Building information
backup=('boot/syslinux/syslinux.cfg')
options=('!makeflags' '!buildflags')
install='syslinux.install'
source=("git://git.kernel.org/pub/scm/boot/syslinux/syslinux.git#tag=\
${pkgname}-${pkgver}"
        'syslinux.cfg'
        'syslinux-install_update'
        'lzo_makefile.patch'
        'install.patch'
        'makefile.sed'
        'com32_makefile.sed')
sha1sums=('SKIP'
          '83a4905632268b0cc7a065230da62d0d5fdc5c00'
          'd22e7e3fc544cf8a49f27ae9a25f17e65f575ea1'
          'be0b8f590b7e513dc44fc4488d1f87e15eba1903'
          'af6a5371e84dc3ab4becbe144bed79af66880e2f'
          '7dc2b414e41ff48b8fc4d58c26cf332c7b97ff5d'
          '8cf5a66b7041979d2fcae05b6b08e1e9bc6bec9c')

prepare() {
	cd "${srcdir}/${pkgname}"

    # Disable debug and development flags to reduce bootloader size
    truncate --size 0 mk/devel.mk

    # Change the makefile in core to _not_ use -MD with nasm
    # Not sure why this is required... but it is.
    # EDIT: This appears to be caused by a nasm bug. See
    # http://bugzilla.nasm.us/show_bug.cgi?id=3392280
    sed -i 'core/Makefile' -e 's:-MD $(dir $@).$(notdir $@).d::'

    # Make prepcore use HOSTCC...
    # This is because, as noted in mk/syslinux.mk, syslinux does not take note
    # of differences between the host and target systems....
    patch 'lzo/Makefile' "${srcdir}/lzo_makefile.patch"
    patch 'Makefile' "${srcdir}/install.patch"


    # Edit the makefile to disable building with extra support...
    # For DOS and suchlike!
    #TODO: Upstream, as a build target?
    # This is mostly from sabotage linux...
    sed -i 'Makefile' -f "${srcdir}/makefile.sed"
    sed -i 'com32/Makefile' "${srcdir}/com32_makefile.sed"

}


build() {
	cd "${srcdir}/${pkgname}"

    # Build
    make PYTHON=python2 ARCH="${CARCH}" CC="${CC}" HOSTCC="${HOSTCC}" \
        INCDIR="${_sysroot}${_includedir}" \
        bios
}


check() {
	cd "${srcdir}/${pkgname}"

    make unittest
}

package() {
	cd "${srcdir}/${pkgname}"

    # Make install
    make bios install INSTALLROOT="${pkgdir}" SBINDIR="${_bindir}" \
        MANDIR='/usr/share/man' AUXDIR="${_libdir}/syslinux"

    # Clean up and install documentation
    install -D -m644 COPYING "${pkgdir}/usr/share/licenses/syslinux/COPYING"
    install -d "${pkgdir}/usr/share/doc"
    cp -ar doc "${pkgdir}/usr/share/doc/syslinux"

    # Install the bios files (MBR, ...)
    install -d "${pkgdir}${_libdir}/syslinux/bios"
    mv "${pkgdir}${_libdir}/syslinux/"{*.bin,*.c32,memdisk} \
       "${pkgdir}${_libdir}/syslinux/bios"

    # Install the default config
    install -D -m0644 '../syslinux.cfg' "${pkgdir}/boot/syslinux/syslinux.cfg"

    # Install the arch-specific update script
    install -D -m0755 '../syslinux-install_update' \
        "${pkgdir}${_bindir}/syslinux-install_update"
}
