# Maintainer: Alastair Hughes < hobbitalastair at yandex dot com>

# Package information
_pkgname='gcc'
pkgname="${_target_triplet}-${_pkgname}"
pkgver='8.2.0'
pkgrel=1
pkgdesc="The GNU Compiler Collection"

# Additional information...
arch=('x86_64')
url='http://gcc.gnu.org/'
license=('GPL' 'LGPL' 'FDL')

# Dependency information
groups=("${_target_triplet}")
depends=("${_target_triplet}-binutils" 'zlib' 'libmpc' 'gmp' 'mpfr')
makedepends=("${_target_triplet}-gcc-static")
targetdepends=("musl" "linux-api-headers")
conflicts=("${_target_triplet}-gcc-static")

# Building information
options=(!emptydirs !strip)
source=("ftp://gcc.gnu.org/pub/gcc/releases/gcc-${pkgver}/gcc-${pkgver}.tar.xz"
        *.patch)
sha256sums=('196c3c04ba2613f893283977e6011b2345d1cd1af9abeac58e916b1aab3e0080'
            '7ad65c8a8613b34b7ffdf0dcee15659ba12a21ed0b65052644d6ca73653d337a'
            '9854b80d6b4854f48438f6784e86411fbc607763a6be80faae5a36c508c3cf9e'
            'c00abb172cd9aee0c951450b4e77d320ea6fd76a2901e96547113c3d0e9bf210')


prepare() {
    cd "${srcdir}/${_pkgname}-${pkgver}"

    # hack! - some configure tests for header files using "$CPP $CPPFLAGS"
    sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" {libiberty,gcc}/configure

    # Disable fixincludes (could lead to header pollution)
    sed -i 's@\./fixinc\.sh@-c true@' gcc/Makefile.in

    # Apply the patches...
    for p in "${srcdir}"/*.patch; do
        patch -p1 < "${p}"
    done
    
    mkdir "${srcdir}/gcc-build"
}


build() {
    cd "${srcdir}/gcc-build"

    # TODO: --enable-cxx-flags is a hack, because currently libstdc++ uses
    #       CXXFLAGS, not CXXFLAGS_FOR_TARGET. See GCC bug 53579...
    export CXXFLAGS="-Os"

    LDFLAGS="-Wl,-rpath,${_toolroot}/lib" \
    "${srcdir}/${_pkgname}-${pkgver}/configure" \
        --build="${_local_triplet}" \
        --host="${_local_triplet}" \
        --target="${_target_triplet}" \
        --prefix="${_toolroot}" \
        --with-sysroot="/sysroot" \
        --with-native-system-header-dir="/usr/include" \
        --with-local-prefix="/sysroot" \
        --libexecdir="/usr/lib" \
        --enable-languages=c,c++ \
        --enable-c99 \
        --enable-__cxa_atexit \
        --enable-threads=posix \
        --enable-libstdcxx-time \
        --enable-cxx-flags="${CXXFLAGS_FOR_TARGET}" \
        --disable-nls \
        --disable-multilib \
        --disable-libcc1 \
        --disable-libsanitizer \
        --disable-libatomic \
        --disable-libmudflap \
        --disable-mudflap \
        --disable-libmpx \
        --with-gnu-as \
        --with-gnu-ld \
        --with-system-zlib

    make
}


package() {
    cd "${srcdir}/gcc-build"

    make DESTDIR="${pkgdir}" install -j1

    # Strip target binaries.
    find "${pkgdir}/usr/lib/gcc/${_target_triplet}/" \
         "${pkgdir}${_toolroot}/lib" \
         -type f -and \( -name \*.a -or -name \*.o \) \
         -exec "${_toolroot}/bin/${_target_triplet}-objcopy" \
         -R .comment -R .note -R .debug_info -R .debug_aranges \
         -R .debug_pubnames -R .debug_pubtypes -R .debug_abbrev -R .debug_line \
         -R .debug_str -R .debug_ranges -R .debug_loc '{}' \;

    # Strip host binaries.
    find "${pkgdir}${_toolroot}/bin/" \
         "${pkgdir}/usr/lib/gcc/${_target_triplet}/" \
         -type f -and \( -executable \) -exec strip '{}' \;

    # Remove info files (already existing).
    rm -r "${pkgdir}${_toolroot}/share/info"
}
