# Maintainer: Alastair Hughes < hobbitalastair at yandex dot com>

# Package information
pkgname='libpng'
pkgver='1.6.31'
pkgrel=3
pkgdesc="A collection of PNG manipulation routines"

# Additional information...
arch=('mips' 'i586')
url="http://www.libpng.org/pub/png/libpng.html"
license=('custom')

# Dependency information
depends=('zlib')

# Building information
source=("http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.xz")
md5sums=('1b34eab440263e32cfa39d19413fad54')


build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    ./configure --prefix="/usr" \
        --disable-shared \
        --enable-static
    make
}


check() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    make check
}


package() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    make DESTDIR="${pkgdir}/" install
    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
