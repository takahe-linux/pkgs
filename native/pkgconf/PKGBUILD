# Maintainer: Alastair Hughes < hobbitalastair at yandex dot com>

# Package information
pkgname='pkgconf'
pkgver='1.0.1'
pkgrel=18
pkgdesc="A drop-in replacement for pkg-config"

# Additional information...
arch=('mips' 'i586')
url="https://github.com/${pkgname}/${pkgname}"
license=('custom')

# Dependency information

provides=('pkg-config')
conflicts=('pkg-config')

# Building information
source=("http://rabbit.dereferenced.org/~nenolod/distfiles/${pkgname}-${pkgver}.tar.gz")
md5sums=('e50254f2f2df97b4f38f9109a869e7e6')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    ./configure --prefix="/usr"
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    make DESTDIR="${pkgdir}/" install

    # We let pkgconf be used as pkg-config.
    ln -s pkgconf "${pkgdir}/usr/bin/pkg-config"

    # Install the license.
    install -Dm0644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
