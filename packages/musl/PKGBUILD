# Maintainer: Alastair Hughes < hobbitalastair at yandex dot com>

# Package information
pkgname='musl'
pkgver='1.1.10'
pkgrel=1
pkgdesc="Lightweight libc implementation"

# Additional information...
arch=('i586' 'mips')
url="http://www.musl-libc.org"
license=('MIT')

# Dependency information
groups=('base')
makedepends=('linux-api-headers')
provides=('libc')
conflicts=('libc')

# Building information
source=("http://www.musl-libc.org/releases/${pkgname}-${pkgver}.tar.gz"
        "uselocale.patch")
md5sums=('fc30892ee582c91920505bbd0021049f'
         '87bafc4f79c7b88261ed7d4f73d4581f')

prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    patch -p1 < "${srcdir}/uselocale.patch"
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    CFLAGS="${_target_cflags}" CXXFLAGS="${CFLAGS}" \
        CROSS_COMPILE="${CROSS_COMPILE}" CC="${CC}" AR="${AR}" AS="${AS}" \
    ./configure --target="${_target_triplet}" \
                --prefix="${_prefix}" \
                --syslibdir="${_libdir}" \
                --libdir="${_libdir}" \
                --bindir="${_bindir}" \
                --includedir="${_includedir}"

    make CC="${CC}" CROSS_COMPILE="${CROSS_COMPILE}"
}


package() {
    cd "${pkgname}-${pkgver}"

    # Install musl
    make DESTDIR="${pkgdir}" install

    # Add a ldd executable
    install -dm755 "${pkgdir}${_bindir}"
    ln -s "${_pkgdir}${_libdir}/libc.so" "${pkgdir}${_bindir}/ldd"

}
