# Maintainer: Alastair Hughes < hobbitalastair at yandex dot com>

# Package information
pkgname='e2fsprogs'
pkgver='1.43.3'
pkgrel=2
pkgdesc='Ext2/3/4 filesystem utilities'

# Additional information...
arch=('mips' 'i586')
url="http://e2fsprogs.sourceforge.net"
license=('GPL' 'LGPL' 'MIT')

# Dependency information
makedepends=('musl' 'ncurses' 'linux-api-headers')
hostdepends=('toolchain')

# Building information
backup=('etc/mke2fs.conf')
source=("http://downloads.sourceforge.net/sourceforge/${_pkgname}/${pkgname}-${pkgver}.tar.gz")
sha1sums=('927cb79c6399c559a83b4fa528ec8912a82d122f')


prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    # TODO: We can't build statically due to -rdynamic...
    sed -i -e 's:-rdynamic::' configure
}


build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    ./configure --prefix="/usr" \
        --build="${_local_triplet}" \
        --host="${_target_triplet}" \
        --sbindir="/usr/bin" \
        --sysconfdir="/etc" \
        --disable-fsck
    make
}


package() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    make DESTDIR="${pkgdir}/" install install-libs
    install -Dm644 "NOTICE" "${pkgdir}/usr/share/licenses/${pkgname}/NOTICE"

    # Make sure awk is awk.
    sed -i -e 's/^AWK=.*/AWK=awk/' "${pkgdir}/usr/bin/compile_et"

    # Remove redundant files.
    rm -rf "${pkgdir}/usr/share/info"
    rm -f "${pkgdir}/usr/lib/charset.alias"
}
