# Maintainer: Alastair Hughes < hobbitalastair at yandex dot com>

# Package information
pkgname='flex'
pkgver='2.6.2'
pkgrel=1
pkgdesc="A tool for generating text-scanning programs"

# Additional information...
arch=('i686' 'x86_64')
url="https://github.com/westes/flex"
license=('custom')

# Dependency information
depends=('sh' 'glibc' 'bison' 'm4')
provides=("${_target_triplet}-${pkgname}")

# Building information
source=("${url}/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.gz"
        "keep-man.patch" # https://github.com/westes/flex/issues/108
       )
md5sums=('cc6d76c333db7653d5caf423a3335239'
         '9b01769868001b84b1c99a9869e50116')


prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    patch -p1 < "${srcdir}/keep-man.patch"
    ./autogen.sh
}


build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    ./configure --prefix="/usr"
    make
}


package() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    make DESTDIR="${pkgdir}/" install
    ln -s flex "${pkgdir}/usr/bin/lex"
    install -Dm644 COPYING \
        "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
