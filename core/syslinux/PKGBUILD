# Maintainer: Alastair Hughes < hobbitalastair at yandex dot com>

# Based of the Arch Linux PKGBUILD

# This PKGBUILD does _not_ support EFI booting...

# Package information
pkgname='syslinux'
pkgver='6.03'
pkgrel=1
pkgdesc='Collection of boot loaders that boot off a variety of mediums'

# Additional information...
arch=('i586') # Syslinux is i386/x86_64 only...
url="http://www.syslinux.org"
license=('GPL2')

# Dependency information
# depends=('bash') # Bash is required for syslinux_install-update script
makedepends=('git' 'python2' 'nasm' 'upx' 'asciidoc')
optdepends=('perl-passwd-md5:  For md5pass'
            'perl-digest-sha1: For sha1pass'
            'mtools:           For mkdiskimage support'
            'util-linux:       For isohybrid'
           )
provides=('bootloader')
conflicts=('bootloader')

# Building information
backup=('boot/syslinux/syslinux.cfg')
options=('!makeflags' '!buildflags')
install='syslinux.install'
source=("git://git.kernel.org/pub/scm/boot/syslinux/syslinux.git#tag=\
${pkgname}-${pkgver}"
        'syslinux.cfg'
        'syslinux-install_update')
sha1sums=('SKIP'
          '83a4905632268b0cc7a065230da62d0d5fdc5c00'
          'd22e7e3fc544cf8a49f27ae9a25f17e65f575ea1')

prepare() {
	cd "${srcdir}/${pkgname}"

    # Disable debug and development flags to reduce bootloader size
    truncate --size 0 mk/devel.mk

    # Change the makefile in core to _not_ use -MD with nasm
    # Not sure why this is required... but it is.
    # EDIT: This appears to be caused by a nasm bug. See
    # http://bugzilla.nasm.us/show_bug.cgi?id=3392280
    sed -i 'core/Makefile' -e 's:-MD $(dir $@).$(notdir $@).d::'
}


build() {
	cd "${srcdir}/${pkgname}"

    make PYTHON=python2 bios
}


check() {
	cd "${srcdir}/${pkgname}"

    make unittest
}

package() {
	cd "${srcdir}/${pkgname}"

    # Make install
    make bios install INSTALLROOT="${pkgdir}" SBINDIR='/usr/bin' \
        MANDIR='/usr/share/man' AUXDIR='/usr/lib/syslinux'

    # Clean up and install documentation
    rm -r "${pkgdir}"/usr/lib/syslinux/{com32,dosutil,syslinux.com}
    install -D -m644 COPYING "${pkgdir}"/usr/share/licenses/syslinux/COPYING
    install -d "$pkgdir"/usr/share/doc
    cp -ar doc "$pkgdir"/usr/share/doc/syslinux

    # Install the bios files (MBR, ...)
    install -d "${pkgdir}/usr/lib/syslinux/bios"
    mv "${pkgdir}"/usr/lib/syslinux/{*.bin,*.c32,*.0,memdisk} \
       "${pkgdir}/usr/lib/syslinux/bios"

    # Install the default config
    install -D -m0644 '../syslinux.cfg' "${pkgdir}/boot/syslinux/syslinux.cfg"

    # Install the arch-specific update script
    install -D -m0755 '../syslinux-install_update' \
        "${pkgdir}/usr/bin/syslinux-install_update"
}
