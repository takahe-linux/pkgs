# Maintainer: Alastair Hughes < hobbitalastair at yandex dot com>

# Package information
pkgname='curl'
pkgver='7.52.1'
pkgrel=1
pkgdesc="An URL retrieval utility and library"

# Additional information...
arch=('mips' 'i586')
url="http://curl.haxx.se"
license=('MIT')

# Dependency information
makedepends=('openssl' 'zlib' 'musl')
hostdepends=('toolchain')

# Building information
source=("http://curl.haxx.se/download/${pkgname}-${pkgver}.tar.gz")
md5sums=('4e1ef056e117b4d25f4ec42ac609c0d4')


build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    ./configure \
        --prefix="/usr" \
        --build="${_local_triplet}" \
        --host="${_target_triplet}" \
        --target="${_target_triplet}" \
        --mandir="/usr/share/man" \
        --disable-ldap \
        --without-libidn \
        --with-ssl \
        --disable-shared \
        --enable-static \
        --with-ca-bundle="/etc/ssl/certs/ca-certificates.crt" \
        --with-ca-path="/etc/ssl/certs"
    make
}


package() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    make DESTDIR="${pkgdir}/" install
    install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
