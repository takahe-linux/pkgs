# Maintainer: Alastair Hughes < hobbitalastair at yandex dot com>

# Package information
_pkgname='musl'
pkgname="${_target_triplet}-libc"
pkgver='1.1.6'
pkgrel=1
pkgdesc="Lightweight libc implementation"

# Additional information...
arch=('i586' 'x86_64')
url="http://www.musl-libc.org"
license=('MIT')

# Dependency information
groups=("${_target_triplet}")
makedepends=("${_target_triplet}-linux-api-headers"
             "${_target_triplet}-${_cc}-static"
             "${_target_triplet}-binutils")

# Building information
source=("http://www.musl-libc.org/releases/${_pkgname}-${pkgver}.tar.gz")
md5sums=('591e2d25a12ca1748e30ee0cf23f8b8a')


build() {
	cd "${srcdir}/${_pkgname}-${pkgver}"

    export CFLAGS="$(echo $CFLAGS | sed s:-fstack-protector-strong::)"

    CROSS_COMPILE="${_target_triplet}-" CC="${_target_triplet}-${_cc}" \
    ./configure --host="${_local_triplet}" \
                --build="${_local_triplet}" \
                --target="${_target_triplet}" \
                --syslibdir="${_libdir}" \
                --prefix="${_prefix}"

    make CC="${_target_triplet}-${_cc}" CROSS_COMPILE="${_target_triplet}-"
}


package() {
    cd "${_pkgname}-${pkgver}"

    make DESTDIR="${pkgdir}${_sysroot}" install
}
