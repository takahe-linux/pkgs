# Maintainer: Alastair Hughes < hobbitalastair at yandex dot com>

# Package information
pkgname='libnl'
pkgver='3.3.0'
pkgrel=2
pkgdesc="Library for applications dealing with netlink sockets"

# Additional information...
arch=('mips' 'i586')
url="http://www.infradead.org/~tgr/libnl/"
license=('GPL')

# Dependency information
makedepends=('linux-api-headers')

# Building information
backup=('etc/libnl/classid' 'etc/libnl/pktloc')
source=("https://github.com/thom311/libnl/releases/download/libnl${pkgver//./_}/${pkgname}-${pkgver}.tar.gz")
md5sums=('ab3ef137cad95bdda5ff0ffa5175dfa5')


prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    sed -i include/linux-private/linux/ipv6.h -e '/#include <linux\/in6.h>/d'
    sed -i include/linux-private/linux/if_bridge.h \
        -e '/#include <linux\/in6.h>/d'
}


build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    ./configure --prefix="/usr" \
        --sysconfdir="/etc" \
        --sbindir="/usr/bin" \
        --disable-shared
    make
}


package() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    make DESTDIR="${pkgdir}/" install
}
