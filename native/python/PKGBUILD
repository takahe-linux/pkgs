# Maintainer: Alastair Hughes < hobbitalastair at yandex dot com>

# Package information
pkgname='python'
pkgver='3.5.4'
_basever='3.5'
pkgrel=24
pkgdesc="High level scripting language"

# Additional information...
arch=('mips' 'i586')
url="http://www.python.org"
license=('custom')

# Dependency information
depends=('expat' 'libffi' 'mpdecimal' 'zlib' 'readline')

# Building information
source=("http://www.python.org/ftp/python/${pkgver}/Python-${pkgver}.tar.xz")
sha1sums=('4aacbd09ca6988255de84a98ab9e4630f584efba')


prepare() {
    cd "${srcdir}/Python-${pkgver}"

    # Look in the right place for python.
    sed -e 's|^#.* /usr/local/bin/python|#!/usr/bin/python|' -i Lib/cgi.py

    # Ensure that we use the system versions.
    rm -r Modules/{expat,zlib,_decimal/libmpdec}
    rm -r Modules/_ctypes/{darwin,libffi}*
}


build() {
    cd "${srcdir}/Python-${pkgver}"

    ./configure --prefix="/usr" \
        --with-system-expat \
        --with-system-ffi \
        --with-system-libmpdec \
        --without-ensurepip \
        --enable-shared

    make
}


package() {
    cd "${srcdir}/Python-${pkgver}"

    make DESTDIR="${pkgdir}/" install maninstall
    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/bin/"idle*

    # Avoid duplicating libpython.
    ln -sf "../../libpython${_basever}m.so" \
        "${pkgdir}/usr/lib/python${_basever}/config-${_basever}m/libpython${_basever}m.so"

    # Provide python.
    ln -s python3 "${pkgdir}/usr/bin/python"
    ln -s python3-config "${pkgdir}/usr/bin/python-config"
    ln -s pydoc3 "${pkgdir}/usr/bin/pydoc"
}
