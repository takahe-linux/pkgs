# Maintainer: Alastair Hughes < hobbitalastair at yandex dot com>

# Package information
pkgname='libedit'
_pkgver='20150325-3.1'
pkgver="$(echo "${_pkgver}" | tr '-' '_')"
pkgrel=1
pkgdesc="Command line editing library"

# Additional information...
arch=('mips' 'i586')
url='http://www.thrysoee.dk/editline/'
license=('BSD')

# Dependency information
# Depends: packages/musl packages/ncurses toolchain/toolchain
makedepends=('ncurses' 'libc')
provides=('readline')
conflicts=('readline')

# Building information
source=("http://www.thrysoee.dk/editline/libedit-${_pkgver}.tar.gz")
sha1sums=('c5bf50b433ea35f9fc883400cde674556c44af30')


build() {
    cd "${srcdir}/${pkgname}-${_pkgver}"

    ./configure --prefix="/usr" --enable-widec \
        --host="${_target_triplet}" \
        --build="${_local_triplet}" \
        --disable-shared
    # __STDC_ISO_10646 required, otherwise libedit complains about size of
    # wchar_t?
    make CFLAGS="${CFLAGS} -D__STDC_ISO_10646__"
}


package() {
    cd "${srcdir}/${pkgname}-${_pkgver}"

    make DESTDIR="${pkgdir}/" install

    # Use libedit instead of readline...
    # Borrowed from Sabotage Linux
    ln -s "libedit.a" "${pkgdir}/usr/lib/libreadline.a"
    mkdir -p "${pkgdir}/usr/include/readline"
    for file in "history" "tilde"; do
        touch "${pkgdir}/usr/include/readline/${file}.h"
    done
    ln -s "/usr/include/editline/readline.h" \
        "${pkgdir}/usr/include/readline/readline.h"

    cp "${pkgdir}/usr/share/man/man3/editline.3" \
        "${pkgdir}/usr/share/man/man3/el.3"
    install -Dm644 COPYING \
        "${pkgdir}/usr/share/licenses/libedit/LICENSE"
}
