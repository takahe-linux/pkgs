# Maintainer: Alastair Hughes < hobbitalastair at yandex dot com>

# Package information
pkgname='git'
pkgver='2.16.1'
pkgrel=9
pkgdesc="A fast distributed version control system"

# Additional information...
arch=('mips' 'i586')
url="https://git-scm.com"
license=('GPL2')

# Dependency information
depends=('curl' 'openssl' 'zlib' 'expat')

# Building information
source=("https://www.kernel.org/pub/software/scm/git/git-${pkgver}.tar.xz")
md5sums=('57b94ffae92d4e8f4c7c46dd95ed99b1')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    make prefix="/usr/" gitexecdir="/usr/lib/git" \
        CC=gcc NEEDS_SSL_WITH_CURL="y" NEEDS_CRYPTO_WITH_SSL="y" NO_PERL="y" \
        CFLAGS="${CFLAGS}" LDFLAGS="${LDFLAGS}" CPPFLAGS="${CPPFLAGS}" \
        NO_CROSS_DIRECTORY_HARDLINKS=1 NO_REGEX="NeedsStartEnd" \
        MAN_BOLD_LITERAL=1 \
        all
}


package() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    make prefix="/usr/" gitexecdir="/usr/lib/git" \
        CC=gcc NEEDS_SSL_WITH_CURL="y" NEEDS_CRYPTO_WITH_SSL="y" NO_PERL="y" \
        CFLAGS="${CFLAGS}" LDFLAGS="${LDFLAGS}" CPPFLAGS="${CPPFLAGS}" \
        NO_CROSS_DIRECTORY_HARDLINKS=1 NO_REGEX="NeedsStartEnd" \
        MAN_BOLD_LITERAL=1 \
        DESTDIR="${pkgdir}/" install

    # Bash completion.
    mkdir -p "${pkgdir}/usr/share/bash-completion/completions/"
    install -m644 ./contrib/completion/git-completion.bash \
        "${pkgdir}/usr/share/bash-completion/completions/git"
}
