# Maintainer: Alastair Hughes < hobbitalastair at yandex dot com>

# Package information
pkgname='micropython'
pkgver='1.8.7'
pkgrel=7
pkgdesc="Minimal implementation of the python scripting language"

# Additional information...
arch=('mips' 'i586')
url="https://www.micropython.org"
license=('MIT')

# Dependency information
makedepends=('musl' 'readline' 'libffi' 'linux-api-headers')
hostdepends=('toolchain' 'python')
provides=('python')
conflicts=('python')

# Building information
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/micropython/micropython/archive/v${pkgver}.tar.gz")
md5sums=('80fd6b88c05ed20886acfd2fb1ea2e11')


prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}/unix"
    cat > mpconfigport.mk << EOF
MICROPY_FORCE_32BIT = 0
MICROPY_USE_READLINE = 1
MICROPY_FATFS = 0
MICROPY_PY_BTREE = 0
MICROPY_PY_THREAD = 1
MICROPY_PY_TERMIOS = 1
MICROPY_PY_SOCKET = 1
MICROPY_PY_FFI = 1
MICROPY_PY_USSL = 0
MICROPY_SSL_AXTLS = 0
MICROPY_SSL_MBEDTLS = 0
MICROPY_PY_JNI = 0
MICROPY_STANDALONE = 0
EOF
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}/unix"
    make CROSS_COMPILE="${_target_triplet}-"
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}/unix"
    make CROSS_COMPILE="${_target_triplet}-" PREFIX="${pkgdir}/usr" install
    install -Dm0644 ../LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    ln -s micropython "${pkgdir}/usr/bin/python"
    ln -s micropython "${pkgdir}/usr/bin/python3"
}
