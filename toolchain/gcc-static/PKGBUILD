# Maintainer: Alastair Hughes < hobbitalastair at yandex dot com>

# Package information
_pkgname='gcc'
pkgname="${_target_triplet}-${_pkgname}-static"
pkgver='7.2.0'
pkgrel=1
pkgdesc="The GNU Compiler Collection"

# Additional information...
arch=('x86_64')
url='http://gcc.gnu.org/'
license=('GPL' 'LGPL' 'FDL')

# Dependency information
depends=("${_target_triplet}-binutils" 'zlib' 'libmpc' 'gmp' 'mpfr')
targetdepends=("linux-api-headers")

# Building information
options=(!emptydirs !strip)
source=("ftp://gcc.gnu.org/pub/gcc/releases/gcc-${pkgver}/gcc-${pkgver}.tar.xz")
sha256sums=('1cf7adf8ff4b5aa49041c8734bbcf1ad18cc4c94d0029aae0f4e48841088479a')


prepare() {
    cd "${srcdir}/${_pkgname}-${pkgver}"

    # hack! - some configure tests for header files using "$CPP $CPPFLAGS"
    sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" {libiberty,gcc}/configure

    # Disable fixincludes (could lead to header pollution)
    sed -i 's@\./fixinc\.sh@-c true@' gcc/Makefile.in

    mkdir "${srcdir}/gcc-build"
}


build() {
    cd "${srcdir}/gcc-build"

    "${srcdir}/${_pkgname}-${pkgver}/configure" \
        --build="${_local_triplet}" \
        --host="${_local_triplet}" \
        --target="${_target_triplet}" \
        --prefix="${_toolroot}" \
        --with-sysroot="/sysroot" \
        --with-native-system-header-dir="/sysroot/usr/include" \
        --with-local-prefix="/sysroot" \
        --libexecdir="/usr/lib" \
        --enable-languages=c \
        --enable-plugins \
        --disable-decimal-float \
        --disable-libffi \
        --disable-libgomp \
        --disable-atomic \
        --disable-libmudflap \
        --disable-libquadmath \
        --disable-libssp \
        --disable-libitm \
        --disable-libstdcxx-pch \
        --disable-nls \
        --disable-shared \
        --disable-threads \
        --disable-multilib \
        --disable-tls \
        --disable-libcc1 \
        --with-gnu-as \
        --with-gnu-ld \
        --with-system-zlib \
        --without-headers \
        --without-cloog \
        --without-isl \
        --with-newlib

    make all-gcc all-target-libgcc
}


package() {
    cd "${srcdir}/gcc-build"

    make DESTDIR="${pkgdir}" install-gcc install-target-libgcc -j1

    # Strip target binaries.
    find "${pkgdir}/usr/lib/gcc/${_target_triplet}/" \
         "${pkgdir}${_toolroot}/lib" \
         -type f -and \( -name \*.a -or -name \*.o \) \
         -exec "${_toolroot}/bin/${_target_triplet}-objcopy" \
         -R .comment -R .note -R .debug_info -R .debug_aranges \
         -R .debug_pubnames -R .debug_pubtypes -R .debug_abbrev -R .debug_line \
         -R .debug_str -R .debug_ranges -R .debug_loc '{}' \;

    # Strip host binaries.
    find "${pkgdir}${_toolroot}/bin/" \
         "${pkgdir}/usr/lib/gcc/${_target_triplet}/" \
         -type f -and \( -executable \) -exec strip '{}' \;

    # Remove info files (already existing).
    rm -r "${pkgdir}${_toolroot}/share/info"
}


