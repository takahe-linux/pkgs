# Maintainer: Alastair Hughes < hobbitalastair at yandex dot com>

# Package information
pkgname='poppler'
pkgver='0.51.0'
pkgrel=1
pkgdesc="PDF rendering library and toolkit"

# Additional information...
arch=('mips' 'i586')
url="https://poppler.freedesktop.org/"
license=('GPL')

# Dependency information
makedepends=('libpng' 'libjpeg' 'pkg-config' 'zlib' 'fontconfig' 'freetype'
    'expat')

# Building information
source=("${url}/${pkgname}-${pkgver}.tar.xz")
md5sums=('342452b9dfd16f3c9912eb029fe0d4e2')


build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    # Use a static PKG_CONFIG.
    export PKG_CONFIG="pkg-config --static"

    ./configure --prefix="/usr" \
        --enable-zlib \
        --enable-libjpeg \
        --enable-libpng \
        --disable-shared
    make
}


check() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make check
}


package() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    make DESTDIR="${pkgdir}/" install
}
