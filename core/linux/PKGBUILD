# Maintainer: Alastair Hughes < hobbitalastair at yandex dot com>

# Based of the Arch Linux linux package

# Edit this line to change the target config (TODO: Make automatic)
_target_config='toshiba_310cds'

# Package information
pkgname="linux-${_target_config}"
_major_version='3.17'
_srcname="linux-${_major_version}"
pkgver="${_major_version}.3"
pkgrel=1
pkgdesc="Linux kernel and modules for '${_target_config}'"

# Additional information...
arch=('i586')
url='http://www.kernel.org'
license=('GPL2')

# Dependency information
groups=('base')
#TODO: Expand!
makedepends=('bc' 'gcc')
provides=('kernel')
conflicts=('kernel')

# Building information
options=('!strip')
source=("https://www.kernel.org/pub/linux/kernel/v3.x/${_srcname}.tar.xz"
        "https://www.kernel.org/pub/linux/kernel/v3.x/patch-${pkgver}.xz"
        "config.${_target_config}"
        *.patch
       )
md5sums=('fb30d0f29214d75cddd2faa94f73d5cf'
         '13f495e3ce72ed6ccefb38591587a6ef'
         'c03d3b9210dff5a6510206070c70c08b'
         'df7fceae6ee5d7e7be7b60ecd7f6bb35'
         'ee9e4d173c2fff74659c862479570d36')


prepare() {
	cd "${srcdir}/${_srcname}"

    # Add upstream patch
    patch -p1 -i "${srcdir}/patch-${pkgver}"

    # Add all of the patches
    for _patch in ${srcdir}/*.patch; do
        patch -p1 -i "${_patch}"
    done

    # Set extraversion to pkgrel
    sed -ri "s|^(EXTRAVERSION =).*|\1 -${pkgrel}|" Makefile

    #TODO: Why?
    # Don't run depmod on 'make install'
    sed -i '2iexit 0' scripts/depmod.sh

    # Clean up the kernel tree
    make mrproper

    # Move the configuration file to it's location
    cat "${srcdir}/config.${_target_config}" > ./.config

    # Make the config
    yes "" | make oldconfig
}


build() {
	cd "${srcdir}/${_srcname}"

    # Make the kernel image
	make bzImage
}


package() {
	cd "${srcdir}/${_srcname}"

    #TODO: Make more flexible
    KARCH=x86

    # Copy the image to the end location
    mkdir -p "${pkgdir}"/boot
    cp arch/${KARCH}/boot/bzImage \
       "${pkgdir}/boot/vmlinuz-${_target_config}"

    # Install the modules (?) (modules_install?)
	make DESTDIR="${pkgdir}/" install
}
