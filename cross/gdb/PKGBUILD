# Maintainer: Alastair Hughes < hobbitalastair at yandex dot com>

# Package information
pkgname='gdb'
pkgver='8.0'
pkgrel=1
pkgdesc='The GNU Debugger'

# Additional information...
arch=('mips' 'i586')
url='http://www.gnu.org/software/gdb/'
license=('GPL3')

# Dependency information
depends=('ncurses')
makedepends=('musl' 'linux-api-headers' 'readline' 'zlib')
hostdepends=('toolchain')

# Building information
backup=('etc/gdb/gdbinit')
source=("http://ftp.gnu.org/gnu/gdb/${pkgname}-${pkgver}.tar.xz"
        "gdb-sgidefs.h"
        "gdb-linux_nat.patch"
        "gnulib-configure.patch")
sha1sums=('148c8e783ebf9b281241d0566db59961191ec64d'
          '20eb85f7adb77b894229998432caae530962316c'
          '7a60c126b1a49cfb554b318a8d7aee1d79fe2bfe'
          'abb3f47ae79c825f8aa76bbb9628afd864a26442')


prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    # hack! - libiberty configure tests for header files using "$CPP $CPPFLAGS"
    sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure

    cp "${srcdir}/gdb-sgidefs.h" "./gdb/sgidefs.h"

    patch -p1 < "${srcdir}/gdb-linux_nat.patch"

    # Work around gnulib trying to use rpl_gettimeofday, which is undefined.
    patch gdb/gnulib/configure < "${srcdir}/gnulib-configure.patch"

    # Stop using --dynamic-list as this produces a dynamic executable.
    sed -i -e "s:['\"]-Wl,--dynamic-list.*['\"]:'-Wl,-static':" gdb/configure
}


build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    ./configure --prefix="/usr" \
        --build="${_local_triplet}" \
        --host="${_target_triplet}" \
        --target="${_target_triplet}" \
        --enable-static \
        --disable-shared \
        --disable-plugins \
        --disable-multilib \
        --disable-werror \
        --disable-nls \
        --disable-gdbserver \
        --with-system-gdbinit=/etc/gdb/gdbinit \
        --with-system-readline \
        --with-system-zlib

    make
}


package() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    make DESTDIR="${pkgdir}/" install

    # Install "custom" system gdbinit.
    install -dm755 "${pkgdir}/etc/gdb"
    touch "${pkgdir}/etc/gdb/gdbinit"

    # Resolve conflicts with binutils.
    rm "${pkgdir}/usr/include"/{ansidecl,bfd,bfdlink,dis-asm,symcat}.h
    rm -r "${pkgdir}/usr/share/info"
    rm "${pkgdir}/usr/lib"/{libbfd,libopcodes}.a

    # Remove /usr/lib as we don't need the remaining library, and charset.alias
    # conflicts with gettext.
    rm -rf "${pkgdir}/usr/lib"
}
