# Maintainer: Alastair Hughes < hobbitalastair at yandex dot com>

# Package information
pkgname='libarchive'
pkgver='3.3.0'
pkgrel=1
pkgdesc="Library for reading and writing several compression formats"

# Additional information...
arch=('i586' 'mips')
url="http://www.libarchive.org"
license=('BSD')

# Dependency information
makedepends=('musl' 'zlib')
hostdepends=('toolchain')

# Building information
source=("http://libarchive.org/downloads/${pkgname}-${pkgver}.tar.gz")
md5sums=('2a2df353a96e132fd04665d17c1561f8')


build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    ./configure --prefix="/usr" \
        --host="${_target_triplet}" \
        --libdir="/usr/lib" \
        --bindir="/usr/bin" \
        --includedir="/usr/include" \
        --without-xml2 \
        --without-iconv \
        --without-nettle \
        --without-expat \
        --disable-shared
    make
}


package() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    make DESTDIR="${pkgdir}/" install

    install -D -m644 'COPYING' "${pkgdir}/usr/share/licenses/libarchive/COPYING"
}
