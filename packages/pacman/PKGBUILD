# Maintainer: Alastair Hughes < hobbitalastair at yandex dot com>

# Based off the Arch Linux PKGBUILD

# Package information
pkgname='pacman'
pkgver='5.0.0'
pkgrel=1
pkgdesc="Arch Linux's simple package manager"

# Additional information...
arch=('i586' 'mips' 'x86_64')
url="http://www.archlinux.org/pacman/"
license=('GPL')

# Dependency information
groups=('base')
# Depends: targets/toolchain packages/musl packages/libarchive packages/bash
# Depends: packages/zlib packages/gettext packages/openssl
depends=('bash' 'libarchive>=3.1.2' 'gettext' 'openssl')
makedepends=('asciidoc' 'zlib' 'libtool=2.4.5' 'libc')
checkdepends=('python2' 'fakechroot')
optdepends=('wget')

# Building information
backup=('etc/pacman.conf' 'etc/makepkg.conf')
source=("https://sources.archlinux.org/other/pacman/${pkgname}-${pkgver}.tar.gz"
        *.patch
        pacman.conf
        makepkg.conf)
md5sums=('9ecf8a5b659c0e02232c945ab198e6e1'
         '6d068025eb54914f343656ed38c33271'
         '6b40bff8bd90882a7d3f6b3024f74d38'
         '03d578816b56852d803cbafac85b9f09')


prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    # Remove AC_FUNC_MALLOC - this is autodetected at runtime so causes a
    # build failure!
    sed -i -e '/AC_FUNC_MALLOC/d' 'configure.ac'

    # Apply my test patch.
    for pat in "${srcdir}/"*.patch; do
        patch -p1 < "${pat}"
    done
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    ./configure --prefix="${_prefix}" \
        --host="${_target_triplet}" \
        --bindir="${_bindir}" \
        --localstatedir='/var' \
        --sysconfdir="${_configdir}" \
        --enable-doc \
        --with-scriptlet-shell="${_bindir}/bash" \
        --without-gpgme \
        --without-openssl \
        --without-curl \
        --without-libcurl \
        --disable-shared

    make LIBS="-lz"
    make -C contrib LIBS="-lz"
}


check() {
    make -C "${srcdir}/${pkgname}-${pkgver}" check
}


package() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    make DESTDIR="${pkgdir}/" install
    make DESTDIR="${pkgdir}/" -C contrib install

    # Install the configuration files
    install -dm755 "${pkgdir}${_configdir}"
    install -m644 "${srcdir}/pacman.conf" "${pkgdir}${_configdir}/pacman.conf"

    # Install and update the makepkg config file
    install -m644 "${srcdir}/makepkg.conf" "${pkgdir}${_configdir}/makepkg.conf"
    sed -i "${pkgdir}${_configdir}/makepkg.conf" \
        -e "s|@CARCH[@]|${_target_arch}|g" \
        -e "s|@CHOST[@]|${_target_triplet}|g" \
        -e "s|@CARCHFLAGS[@]|-march=${_target_arch}|g"

    # Install the bash completion files
    install -dm755 "${pkgdir}${_bashcompletiondir}"
    mv "${pkgdir}${_configdir}/bash_completion.d/pacman" \
        "${pkgdir}${_bashcompletiondir}"
    rmdir "${pkgdir}${_configdir}/bash_completion.d"

    for file in makepkg pacman-key; do
        ln -s 'pacman' "${pkgdir}${_bashcompletiondir}/${file}"
    done

    install -Dm644 'contrib/PKGBUILD.vim' \
        "${pkgdir}${_vimdir}/vimfiles/syntax/PKGBUILD.vim"
}
