# Maintainer: Alastair Hughes < hobbitalastair at yandex dot com>

# Package information
pkgname='libjpeg'
_pkgname='libjpeg-turbo'
pkgver='1.5.1'
pkgrel=10
pkgdesc="JPEG image codec"

# Additional information...
arch=('mips' 'i586')
url="http://libjpeg-turbo.virtualgl.org/"
license=('custom')

# Dependency information
makedepends=('nasm')

# Building information
source=("http://downloads.sourceforge.net/project/${_pkgname}/${pkgver}/${_pkgname}-${pkgver}.tar.gz"
        "mips-simd.patch")
sha1sums=('ebb3f9e94044c77831a3e8c809c7ea7506944622'
          '3d47c73c71583e346ef8ef10b86f54fef2fe5668')


prepare() {
    cd "${srcdir}/${_pkgname}-${pkgver}"

    # Fix build on mips - remove with next release.
    patch -p0 < "${srcdir}/mips-simd.patch"
}


build() {
    cd "${srcdir}/${_pkgname}-${pkgver}"

    ./configure --prefix="/usr" \
        --mandir="/usr/share/man" \
        --disable-shared \
        --enable-static \
        --with-jpeg8
    make
}


check() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    make test
}


package() {
    cd "${srcdir}/${_pkgname}-${pkgver}"

    make DESTDIR="${pkgdir}/" docdir="/usr/share/doc/${pkgname}" \
        exampledir="/usr/share/doc/${pkgname}" install
    install -d "${pkgdir}/usr/share/licenses/${pkgname}"
    ln -s -f ../../doc/libjpeg/README "${pkgdir}/usr/share/licenses/${pkgname}"
}
