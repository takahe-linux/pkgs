# Maintainer: Alastair Hughes < hobbitalastair at yandex dot com>

# Package information
pkgname='binutils'
pkgver='2.30'
pkgrel=4
pkgdesc="A set of programs to assemble and manipulate binary and object files"

# Additional information...
arch=('mips' 'i586')
url="http://www.gnu.org/software/binutils/"
license=('GPL')

# Dependency information
groups=('base-devel')
depends=('musl' 'zlib')
hostdepends=('toolchain')

# Building information
options=('staticlibs' '!distcc' '!ccache')
source=("https://sourceware.org/pub/${pkgname}/releases/${pkgname}-${pkgver}.tar.xz")
md5sums=('ffc476dd46c96f932875d1b2e27e929f')


prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure

    mkdir "${srcdir}/${pkgname}-build"
}


build() {
    cd "${srcdir}/${pkgname}-build"

    "${srcdir}/${pkgname}-${pkgver}/configure" \
        --prefix="/usr" \
        --build="${_local_triplet}" \
        --host="${_target_triplet}" \
        --target="${_target_triplet}" \
        --disable-nls --disable-multilib \
        --disable-werror --enable-shared \
        --with-lib-path="/usr/lib" \
        --enable-deterministic-archives

    make configure-host
    make
}


package() {
    cd "${srcdir}/${pkgname}-build"

    make prefix="${pkgdir}/usr" install

    # Remove manpages for MS Windows tools
    rm "${pkgdir}/usr/share/man/man1/"{dlltool,nlmconv,windres,windmc}*

    # Remove the info files - I'm not using them
    rm -r "${pkgdir}/usr/share/info"
}
