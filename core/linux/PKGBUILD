# Maintainer: Alastair Hughes < hobbitalastair at yandex dot com>

# Based of the Arch Linux linux package

# Edit this line to change the target config (TODO: Make automatic)
_target_config='toshiba_310cds'

# Package information
pkgname="linux-${_target_config}"
_major_version='3.17'
_srcname="linux-${_major_version}"
pkgdesc="Linux kernel and modules for '${_target_config}'"
pkgver="${_major_version}.3"
pkgrel=1

# Additional information...
arch=('i586')
url='http://www.kernel.org'
license=('GPL2')

# Dependency information
groups=('base')
#TODO: Expand!
makedepends=('bc' 'gcc')
provides=('kernel')
conflicts=('kernel')

# Building information
options=('!strip')
source=("https://www.kernel.org/pub/linux/kernel/v3.x/${_srcname}.tar.xz"
        "https://www.kernel.org/pub/linux/kernel/v3.x/patch-${pkgver}.xz"
        "config.${_target_config}"
        *.patch
       )
md5sums=('fb30d0f29214d75cddd2faa94f73d5cf'
         '13f495e3ce72ed6ccefb38591587a6ef'
         'c03d3b9210dff5a6510206070c70c08b'
         'df7fceae6ee5d7e7be7b60ecd7f6bb35'
         'ee9e4d173c2fff74659c862479570d36')

prepare() {
	cd "${srcdir}/${_srcname}"

    # Add upstream patch
    patch -p1 -i "${srcdir}/patch-${pkgver}"

    # Add all of the patches
    for _patch in ${srcdir}/*.patch; do
        patch -p1 -i "${_patch}"
    done

    # Set extraversion to pkgrel
    sed -ri "s|^(EXTRAVERSION =).*|\1 -${pkgrel}|" Makefile

    # Clean up the kernel tree
    make mrproper

    # Move the configuration file to it's location
    cat "${srcdir}/config.${_target_config}" > ./.config

    # Make the config
    yes "" | make oldconfig
}


build() {
	cd "${srcdir}/${_srcname}"

    # Make the kernel image and modules, if needed
    if [ $(cat "${srcdir}/config.${_target_config}" | grep CONFIG_MODULES=y) ]
        then
        # Modules are enabled
        make ${MAKEFLAGS} bzImage modules
    else
        # Modules are not enabled
        make ${MAKEFLAGS} bzImage
    fi
}


package() {
    # Main Linux kernel package

	cd "${srcdir}/${_srcname}"

    # Check for module support
    [ $(cat "${srcdir}/config.${_target_config}" | grep CONFIG_MODULES=y) ] && \
        MODULES=true || MODULES=false

    #TODO: Make more flexible
    KARCH='x86'

    # Find the kernel version
    _kernver="$(make kernelrelease)"

    # Make the installation directories
    mkdir -p "${pkgdir}"/{/usr/lib/modules,/usr/lib/firmware,boot}

    # Copy the image to the end location
    cp "arch/${KARCH}/boot/bzImage" \
       "${pkgdir}/boot/vmlinuz-${_target_config}"

    # Install the modules and firmware, if required
    if ${MODULES}; then
        make INSTALL_MOD_PATH="${pkgdir}/" modules_install
        # Clean up kernel source tree
        rm -f "${pkgdir}/lib/modules/${_kernver}"/{source,build}
        # Add vmlinux
        install -D -m644 'vmlinux' \
            "${pkgdir}/usr/lib/modules/${_kernver}/build/vmlinux" 
    fi

}

