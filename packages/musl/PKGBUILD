# Maintainer: Alastair Hughes < hobbitalastair at yandex dot com>

# Package information
pkgname='musl'
pkgver='1.1.11'
pkgrel=1
pkgdesc="Lightweight libc implementation"

# Additional information...
arch=('i586' 'mips')
url="http://www.musl-libc.org"
license=('MIT')

# Dependency information
groups=('base')
# Depends: toolchain/gcc-static toolchain/binutils packages/linux-api-headers
makedepends=('linux-api-headers')
provides=('libc')
conflicts=('libc')

# Building information
source=("http://www.musl-libc.org/releases/${pkgname}-${pkgver}.tar.gz")
md5sums=('48be0777e32f374d387e9cf85e36ec4d')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    CFLAGS="${_target_cflags}" CXXFLAGS="${CFLAGS}" \
    ./configure --target="${_target_triplet}" \
                --prefix="${_prefix}" \
                --syslibdir="${_libdir}" \
                --libdir="${_libdir}" \
                --bindir="${_bindir}" \
                --includedir="${_includedir}" \
                --disable-shared

    make
}


package() {
    cd "${pkgname}-${pkgver}"

    # Install musl
    make DESTDIR="${pkgdir}" install
}
