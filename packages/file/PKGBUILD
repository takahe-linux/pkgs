# Maintainer: Alastair Hughes < hobbitalastair at yandex dot com>

# Package information
pkgname='file'
pkgver='5.28'
# TODO: Remove workaround for config.guess in next update.
pkgrel=1
pkgdesc='File type identification utility'

# Additional information...
arch=('mips' 'i586')
url='http://www.darwinsys.com/file/'
license=('custom')

# Dependency information
groups=('base')
makedepends=('zlib' 'musl')
hostdepends=('toolchain' "${_target_triplet}-file")

# Building information
source=("ftp://ftp.astron.com/pub/${pkgname}/${pkgname}-${pkgver}.tar.gz")
md5sums=('3f7771424aa855f32094b49571e19b33')


build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    ./configure --prefix="/usr" \
        --host="$(echo "${_target_triplet}" | sed 's:musl:gnu:')" \
        --build="${_local_triplet}" \
        --disable-shared
    make
}


package() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    make DESTDIR="${pkgdir}/" install
    install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
    rmdir "${pkgdir}/usr/share/man/man5"
}

