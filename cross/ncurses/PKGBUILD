# Maintainer: Alastair Hughes < hobbitalastair at yandex dot com>

# Package information
pkgname='ncurses'
pkgver='6.0'
pkgrel=9
pkgdesc='System V Release 4.0 curses emulation library'

# Additional information...
arch=('i586' 'mips')
url='http://invisible-island.net/ncurses/ncurses.html'
license=('MIT')

# Dependency information
makedepends=('musl')
hostdepends=('toolchain')

# Building information
source=("ftp://invisible-island.net/ncurses/ncurses-${pkgver}.tar.gz")
md5sums=('ee13d052e1ead260d7c28071f46eefb1')


build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    ./configure --prefix="/usr" \
        --host="${_target_triplet}" \
        --mandir="/usr/share/man" \
        --enable-overwrite \
        --disable-shared \
        --disable-db-install \
        --with-build-cc="gcc" \
        --with-fallbacks="ansi linux vt100 xterm" \
        --without-debug \
        --without-tests \
        --without-ada \
        --without-cxx \
        --without-cxx-binding
    make
}


package() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    make DESTDIR="${pkgdir}/" install

    # Install license, rip it from the readme
    install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
    grep -B 100 '$Id' README > "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
