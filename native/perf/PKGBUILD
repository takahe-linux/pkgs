# Maintainer: Alastair Hughes < hobbitalastair at yandex dot com>

# Package information
pkgname="perf"
_major_version='4.15'
_srcname="linux-${_major_version}"
pkgdesc="Perf utilities"
pkgver="${_major_version}.1"
pkgrel=2

# Additional information...
arch=('i586')
url='http://www.kernel.org'
license=('GPL2')

# Dependency information
depends=('elfutils')

# Building information
source=("https://www.kernel.org/pub/linux/kernel/v4.x/${_srcname}.tar.xz"
        "https://www.kernel.org/pub/linux/kernel/v4.x/patch-${pkgver}.xz"
        )
md5sums=('0d701ac1e2a67d47ce7127432df2c32b'
         '839159e4710e11dcc836023b3d7b1bf7')

prepare() {
    cd "${srcdir}/${_srcname}"
    patch -p1 -i "${srcdir}/patch-${pkgver}"

    # We don't need iberty for libbfd support - disable.
    sed -i tools/build/feature/Makefile -e 's: -liberty : :g'
    sed -i tools/perf/Makefile.config -e 's: -liberty::g'
}


build() {
    cd "${srcdir}/${_srcname}/tools/perf"
    make -f Makefile.perf \
        prefix=/usr lib=lib/perf perfexecdir=lib/perf \
        DESTDIR="${pkgdir}"
}


package() {
    cd "${srcdir}/${_srcname}/tools/perf"
    make -f Makefile.perf \
        prefix=/usr lib=lib/perf perfexecdir=lib/perf \
        DESTDIR="${pkgdir}" \
        install
    rm -rf "${pkgdir}/etc/bash_completion.d"
}
