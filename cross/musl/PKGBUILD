# Maintainer: Alastair Hughes < hobbitalastair at yandex dot com>

# Package information
pkgname='musl'
pkgver='1.1.18'
pkgrel=2
pkgdesc="Lightweight libc implementation"

# Additional information...
arch=('i586' 'mips')
url="http://www.musl-libc.org"
license=('MIT')

# Dependency information
groups=('base-devel')
hostdepends=("${_target_triplet}-gcc-static" "${_target_triplet}-binutils")

# Building information
source=("http://www.musl-libc.org/releases/${pkgname}-${pkgver}.tar.gz")
md5sums=('5ff5f669dd248f00df77c19f575d2039')


build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    ./configure --target="${_target_triplet}" \
                --prefix="/usr" \
                --syslibdir="/usr/lib" \
                --libdir="/usr/lib" \
                --bindir="/usr/bin" \
                --includedir="/usr/include" \
                --disable-static

    make
}


package() {
    cd "${pkgname}-${pkgver}"

    # Install musl
    make DESTDIR="${pkgdir}" install
    mkdir "${pkgdir}/usr/bin"
    ln -s /usr/lib/libc.so "${pkgdir}/usr/bin/ldd"

    # Rename all the empty static libraries to dynamic equivalents.
    for lib in "${pkgdir}/usr/lib/"*.a; do
        mv -f "${lib}" "${lib%a}so"
    done
}
