# Maintainer: Alastair Hughes < hobbitalastair at yandex dot com>

# Package information
# This is named e2fsprogs-util as busybox provides e2fsprogs.
pkgname='e2fsprogs-util'
_pkgname='e2fsprogs'
pkgver='1.43.1'
pkgrel=1
pkgdesc='Ext2/3/4 filesystem utilities'

# Additional information...
arch=('mips' 'i586')
url="http://e2fsprogs.sourceforge.net"
license=('GPL' 'LGPL' 'MIT')

# Dependency information
makedepends=('musl' 'ncurses' 'linux-api-headers')
hostdepends=('toolchain')

# Building information
backup=('etc/mke2fs.conf')
source=("http://downloads.sourceforge.net/sourceforge/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")
sha1sums=('26b75c27ba434e72ef630b160782a01b4d992b7a')


build() {
    cd "${srcdir}/${_pkgname}-${pkgver}"

    ./configure --prefix="/usr" \
        --build="${_local_triplet}" \
        --host="${_target_triplet}" \
        --sbindir="/usr/bin" \
        --sysconfdir="/etc"
    make
}


package() {
    cd "${srcdir}/${_pkgname}-${pkgver}"

    make DESTDIR="${pkgdir}/" install install-libs
    install -Dm644 "NOTICE" "${pkgdir}/usr/share/licenses/${pkgname}/NOTICE"

    # Make sure awk is awk
    sed -i -e 's/^AWK=.*/AWK=awk/' "${pkgdir}/usr/bin/compile_et"
    # Remove references to build directory
    sed -i -e 's:^SS_DIR=.*:SS_DIR="/usr/share/ss":' \
        "${pkgdir}/usr/bin/mk_cmds"
    sed -i -e 's:^ET_DIR=.*:ET_DIR="/usr/share/et":' \
        "${pkgdir}/usr/bin/compile_et"

    # Remove redundant files.
    rm -rf "${pkgdir}/usr/share/info"
    rm -f "${pkgdir}/usr/lib/charset.alias"

    # Remove applets busybox provides.
    rm -f "${pkgdir}/usr/bin/"{fsck,chattr,lsattr,tune2fs,blkid}
}
