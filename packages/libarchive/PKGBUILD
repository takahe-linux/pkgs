# Maintainer: Alastair Hughes < hobbitalastair at yandex dot com>

# Based off the Arch Linux PKGBUILD

# Package information
pkgname='libarchive'
pkgver='3.1.2'
pkgrel=1
pkgdesc="Library for reading and writing several compression formats"

# Additional information...
arch=('i586' 'mips')
url="http://www.libarchive.org"
license=('BSD')

# Dependency information
makedepends=('musl' 'zlib')
hostdepends=('toolchain')

# Building information
options=() # Originally 'strip', 'debug', and 'libtool'
source=("http://libarchive.org/downloads/$pkgname-$pkgver.tar.gz"
        '0001-mtree-fix-line-filename-length-calculation.patch'
        '0001-Limit-write-requests-to-at-most-INT_MAX.patch'
        'libarchive-3.1.2-acl.patch'
        'libarchive-3.1.2-sparce-mtree.patch')
md5sums=('efad5a503f66329bb9d2f4308b5de98a'
         'fda89c145bbcd793a96b06b463ef6a72'
         '9bf80940bd3ce861137a0a8dcacf5705'
         'a5c995661c62429ceff2c23ea322393b'
         'cb344a879b3c4550fe3faf86c3826f23')


prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    for PATCH in ${srcdir}/*.patch; do
        patch -Np1 -i "${PATCH}"
    done
}


build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    export LDFLAGS="${LDFLAGS} -static"

    ./configure --prefix="/usr" \
        --host="${_target_triplet}" \
        --libdir="/usr/lib" \
        --bindir="/usr/bin" \
        --includedir="/usr/include" \
        --without-xml2 \
        --without-iconv \
        --without-nettle \
        --without-expat \
        --disable-shared
    make
}


check() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    make -k check
}


package() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    make DESTDIR="${pkgdir}/" install

    install -D -m644 'COPYING' "${pkgdir}/usr/share/licenses/libarchive/COPYING"
}
