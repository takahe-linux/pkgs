# Maintainer: Alastair Hughes < hobbitalastair at yandex dot com>

# Package information
pkgname='xz'
pkgver='5.2.4'
pkgrel=3
pkgdesc="Library for dealing with XZ and LZMA compressed files"

# Additional information...
arch=('mips' 'i586')
url="http://tukaani.org/xz"
license=('GPL' 'LGPL' 'custom')

# Dependency information
groups=('base-devel')
depends=('musl')
hostdepends=('toolchain')

# Building information
source=("http://tukaani.org/${pkgname}/${pkgname}-${pkgver}.tar.gz")
md5sums=('5ace3264bdd00c65eeec2891346f65e6')


build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    ./configure --prefix="/usr" \
        --host="${_target_triplet}" \
        --build="${_local_triplet}" \
        --disable-rpath
    make
}


package() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    make DESTDIR="${pkgdir}/" install
    install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}/"
    ln -sf "/usr/share/doc/${pkgname}/COPYING" \
        "${pkgdir}/usr/share/licenses/${pkgname}/"
    ln -sf "/usr/share/licenses/common/GPL2/license.txt" \
        "${pkgdir}/usr/share/doc/${pkgname}/COPYING.GPLv2"
}
