# Maintainer: Alastair Hughes < hobbitalastair at yandex dot com>

# Package information
_pkgname='gcc'
pkgname="${_target_triplet}-${_pkgname}-static"
pkgver='6.1.0'
pkgrel=1
pkgdesc="The GNU Compiler Collection"

# Additional information...
arch=('x86_64')
url='http://gcc.gnu.org/'
license=('GPL' 'LGPL' 'FDL')

# Dependency information
depends=("${_target_triplet}-binutils" 'zlib' 'libmpc' 'gmp' 'mpfr')
targetdepends=("linux-api-headers")

# Building information
options=(!emptydirs !strip)
source=("ftp://gcc.gnu.org/pub/gcc/releases/gcc-${pkgver}/gcc-${pkgver}.tar.bz2")
sha256sums=('09c4c85cabebb971b1de732a0219609f93fc0af5f86f6e437fd8d7f832f1a351')


prepare() {
    cd "${srcdir}/${_pkgname}-${pkgver}"

    echo "${pkgver}" > gcc/BASE-VER

    # hack! - some configure tests for header files using "$CPP $CPPFLAGS"
    sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" {libiberty,gcc}/configure

    # Disable fixincludes (could lead to header pollution)
    sed -i 's@\./fixinc\.sh@-c true@' gcc/Makefile.in

    mkdir "${srcdir}/gcc-build"
}


build() {
    cd "${srcdir}/gcc-build"

    export CFLAGS_FOR_TARGET="${_target_cflags} --sysroot=/sysroot"
    export CXXFLAGS_FOR_TARGET="${CFLAGS_FOR_TARGET}"
    export CPPFLAGS_FOR_TARGET="${_target_cppflags} -I/sysroot/usr/include"

    "${srcdir}/${_pkgname}-${pkgver}/configure" \
        --build="${_local_triplet}" \
        --host="${_local_triplet}" \
        --target="${_target_triplet}" \
        --prefix="${_toolroot}" \
        --with-sysroot="/sysroot" \
        --with-native-system-header-dir="/sysroot/usr/include" \
        --with-local-prefix="/sysroot" \
        --libexecdir="/usr/lib" \
        --enable-languages=c \
        --enable-plugins \
        --disable-decimal-float \
        --disable-libffi \
        --disable-libgomp \
        --disable-atomic \
        --disable-libmudflap \
        --disable-libquadmath \
        --disable-libssp \
        --disable-libitm \
        --disable-libstdcxx-pch \
        --disable-nls \
        --disable-shared \
        --disable-threads \
        --disable-multilib \
        --disable-tls \
        --with-gnu-as \
        --with-gnu-ld \
        --with-system-zlib \
        --without-headers \
        --without-cloog \
        --without-isl \
        --with-newlib

    make all-gcc all-target-libgcc
}


package() {
    cd "${srcdir}/gcc-build"

    #TODO: Split this into two packages, one for each architecture.

    make DESTDIR="${pkgdir}" install-gcc install-target-libgcc -j1

    # strip target binaries
    find "${pkgdir}/usr/lib/gcc/${_target_triplet}/" \
         "${pkgdir}${_toolroot}/lib" \
         -type f -and \( -name \*.a -or -name \*.o \) \
         -exec "${_toolroot}/bin/${_target_triplet}-objcopy" \
         -R .comment -R .note -R .debug_info -R .debug_aranges \
         -R .debug_pubnames -R .debug_pubtypes -R .debug_abbrev -R .debug_line \
         -R .debug_str -R .debug_ranges -R .debug_loc '{}' \;

    # strip host binaries
    find "${pkgdir}${_toolroot}/bin/" \
         "${pkgdir}/usr/lib/gcc/${_target_triplet}/" \
         -type f -and \( -executable \) -exec strip '{}' \;

    # Remove info files (already existing)
    rm -r "${pkgdir}${_toolroot}/share/info"
}


