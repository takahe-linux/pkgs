# Maintainer: Alastair Hughes < hobbitalastair at yandex dot com>

# Package information
pkgname=('busybox' 'mdev' 'hush' 'bb-coreutils' 'bb-util-linux' 'bb-findutils')
pkgver='1.24.2'
pkgrel=1

# Additional information...
arch=('i586' 'mips')
url="https://www.busybox.net"
license=('GPL') # Busybox; not sure about the service files, which 
                # are from sabotage linux

# Dependency information
makedepends=('musl' 'linux-api-headers')
hostdepends=('toolchain')

# Building information
_web_address="${url}/downloads"
_patch_address="${_web_address}/fixes-${pkgver}"
source=("${_web_address}/${pkgname}-${pkgver}.tar.bz2"
        "config"
       )
md5sums=('2eaae519cac1143bcf583636a745381f'
         '1077b807254484f8ee2b5e2ff84e7024')


prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    # Adjust the config for the current settings
    sed -i -e "/CONFIG_CROSS_COMPILER_PREFIX/s:=\".*\":=\"${_target_triplet}-\":" \
        "${srcdir}/config"
    sed -i -e "/CONFIG_SYSROOT/s:=\".*\":=\"/sysroot\":" "${srcdir}/config"

}


build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    # Install the default config
    cp "${srcdir}/config" ".config"

    # ???
    sed '1,1i#include <sys/resource.h>' -i include/libbb.h

    # If you want to run menuconfig uncomment the following line:
    #make menuconfig; return 1

    make ARCH="${_target_arch}" \
        CC="${CC}" HOSTCC="${HOSTCC}" \
        HOSTCFLAGS="${HOSTCFLAGS}"

}


package_busybox() {
    pkgdesc="Main busybox binary and symlinks"
    groups=('bb')
    _pkg_list=('kbd' 'ncurses' 'which' 'gzip' 'tar' 'bzip2' 'awk' \
        'diffutils' 'sed' 'vi' 'patch' 'shadow' 'e2fsprogs' 'less' 'hdparm' \
        'man' 'inetutils' 'ip2route' 'iputils' 'dhcpcd' 'procps-ng' 'xz')
    provides=(${_pkg_list}[@])
    conflicts=(${_pkg_list}[@])

    cd "${srcdir}/${pkgname}-${pkgver}"

    ARCH="${_target_arch}" make DESTDIR="${pkgdir}/" install

    # Copy the busybox binary into place
    install -Dm755 "_install/bin/busybox" "${pkgdir}/usr/bin/busybox"


    #TODO: Figure out what here is actually provided by kbd
    KBD="deallocvt dumpkmap loadkmap setconsole setkeycodes showkey kbd_mode"
    WHICH="which"

    APPLETS="gunzip gzip lzop tar bzip2 bunzip2 unxz bzcat lzcat lzopcat \
        unlzma unlzop xzcat zcat [ [[ uevent xz"
    EDITORS="awk cmp diff patch sed vi"
    SHADOW="adduser addgroup deluser delgroup passwd"
    EXT2="chattr fsck lsattr tune2fs"

    MISC="less hdparm man"
    NET="ping hostname ip udhcpc ipaddr iplink dnsdomainname"
    PROC="iostat lsof nmeter pmap pstree top free fuser kill killall pgrep \
        pidof pkill ps renice watch sysctl"

    _link_packages $KBD $NCURSES $WHICH $APPLETS $EDITORS $LOGIN $EXT2 $MISC $NET $PROC $SHADOW
}

package_mdev() {
    pkgdesc="Lightweight udev alternative, from busybox"
    depends=("busybox")
    groups=('bb')

    MDEV="mdev"

    _link_packages $MDEV
}

package_hush() {
    pkgdesc="Simple busybox shell"
    depends=("busybox")
    groups=('bb')

    SHELLS="hush"

    _link_packages $SHELLS
}

package_bb-coreutils() {
    pkgdesc="Coreutils symlinks for busybox"
    groups=('bb')
    depends=('busybox')
    provides=('coreutils')
    conflicts=('coreutils')

    COREUTILS="basename cat date id groups shuf test touch tr who chgrp \
        chmod chown chroot cksum comm cp cut dd df dirname du echo env expand \
        expr false fsync head install ln logname ls md5sum mkdir mkfifo \
        mknod mv nice nohup printf pwd readlink realpath rm rmdir seq \
        sha1sum sha256sum sha512sum sha3sum sleep sort stat sync tac \
        tail tee true tty uname unexpand uniq usleep uudecode uuencode wc \
        whoami yes timeout rev mktemp od"

    _link_packages $COREUTILS
    
}

package_bb-util-linux() {
    pkgdesc="util linux symlinks for busybox"
    groups=('bb')
    depends=('busybox' 'hush')
    provides=('util-linux')
    conflicts=('util-linux')

    UTILLINUX="blockdev blkid dmesg fbset getopt hwclock losetup lspci lsusb \
        mount swapon swapoff umount su"
    LOGIN="getty login"
    MISC_UTILS="ionice mountpoint setsid time"

    _link_packages $UTILLINUX $LOGIN $MISC_UTILS

    # Add a custom nologin script.
    # There is a nologin shell script provided with busybox, which we ignore
    # (it is pretty basic, and uses /bin/sh, not /usr/bin/sh).
    cat > "${pkgdir}/usr/bin/nologin" << EOF
#!/usr/bin/sh
if [ -f /etc/nologin.conf ]; then
    cat /etc/nologin.conf
else
    echo 'login not enabled'
fi

exit 1
EOF
    chmod 0755 "${pkgdir}/usr/bin/nologin"

}

package_bb-findutils() {
    pkgdesc="findutils symlinks for busybox"
    groups=('bb')
    depends=('busybox')
    provides=('findutils')
    conflicts=('findutils')

    FIND="find grep xargs"

    _link_packages $FIND
}

_link_packages() {

    install -dm755 "${pkgdir}/usr/bin"
    
    for package in $@; do
        ln -s "busybox" "${pkgdir}/usr/bin/${package}"
    done
}
