# Maintainer: Alastair Hughes < hobbitalastair at yandex dot com>

# Based off the Arch Linux pkgbuild

# Package information
pkgname='mpfr'
_pkgver='3.1.3'
_patchlevel='p4'
pkgver="${_pkgver}.${_patchlevel}"
pkgrel=1
pkgdesc="Multiple-precision floating-point library"

# Additional information...
arch=('mips')
url="http://www.mpfr.org/"
license=('LGPL')

# Dependency information
# Depends: packages/gmp targets/toolchain packages/musl
makedepends=('gmp>=5.0' 'libc')

# Building information
source=("http://www.mpfr.org/mpfr-current/${pkgname}-${_pkgver}.tar.xz"
        "${pkgname}-${pkgver}.patch::http://www.mpfr.org/mpfr-current/allpatches")
md5sums=('6969398cd2fbc56a6af570b5273c56a9'
         'e502185ebb22b41c528f183bb22a7569')


prepare() {
    cd "${srcdir}/${pkgname}-${_pkgver}"

    # Add linux-musl as a system to config.sub
    sed -i -e 's:linux-uclibc\* |:linux-musl\* | linux-uclibc\* |:' 'config.sub'
    sed -i -e 's:| -linux-uclibc\*:| -linux-musl\* | -linux-uclibc\*:' 'config.sub'

    # Add the patchlevel version patch
    patch -N -Z -p1 < "${srcdir}/${pkgname}-${pkgver}.patch"
}


build() {
    cd "${srcdir}/${pkgname}-${_pkgver}"

    ./configure --prefix="${_prefix}" \
        --build="${_local_triplet}" \
        --host="${_target_triplet}" \
        --disable-shared

    make
}


package() {
    cd "${srcdir}/${pkgname}-${_pkgver}"

    make DESTDIR="${pkgdir}/" install

    # Remove info files 
    rm -rf "${pkgdir}${_prefix}/share/info"
}
