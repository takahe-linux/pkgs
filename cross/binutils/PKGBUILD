# Maintainer: Alastair Hughes < hobbitalastair at yandex dot com>

# Package information
pkgname='binutils'
pkgver='2.29.1'
pkgrel=5
pkgdesc="A set of programs to assemble and manipulate binary and object files"

# Additional information...
arch=('mips' 'i586')
url="http://www.gnu.org/software/binutils/"
license=('GPL')

# Dependency information
groups=('base-devel')
depends=('musl' 'zlib')
hostdepends=('toolchain')

# Building information
options=('!distcc' '!ccache')
source=("ftp://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.bz2")
md5sums=('9af59a2ca3488823e453bb356fe0f113')


prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure

    mkdir "${srcdir}/${pkgname}-build"
}


build() {
    cd "${srcdir}/${pkgname}-build"

    "${srcdir}/${pkgname}-${pkgver}/configure" \
        --prefix="/usr" \
        --build="${_local_triplet}" \
        --host="${_target_triplet}" \
        --target="${_target_triplet}" \
        --disable-nls --disable-multilib \
        --disable-werror \
        --with-lib-path="/usr/lib" \
        --enable-deterministic-archives

    make configure-host
    make
}


package() {
    cd "${srcdir}/${pkgname}-build"

    make prefix="${pkgdir}/usr" install

    # Remove manpages for MS Windows tools
    rm "${pkgdir}/usr/share/man/man1/"{dlltool,nlmconv,windres,windmc}*

    # Remove the info files - I'm not using them
    rm -r "${pkgdir}/usr/share/info"
}
