# Maintainer: Alastair Hughes < hobbitalastair at yandex dot com>

# Package information
pkgname='file'
pkgver='5.30'
pkgrel=1
pkgdesc='File type identification utility'

# Additional information...
arch=('i686' 'x86_64')
url='http://www.darwinsys.com/file/'
license=('custom')

# Dependency information
depends=('glibc' 'zlib')
provides=("${_target_triplet}-${pkgname}")

# Building information
source=("ftp://ftp.astron.com/pub/${pkgname}/${pkgname}-${pkgver}.tar.gz")
md5sums=('f35eaf05489ae566eafc4d26bb1dcd90')


build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    export CFLAGS="${CFLAGS} -static"

    ./configure --prefix="/usr" \
        --enable-static \
        --disable-shared
    make
}


package() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    make DESTDIR="${pkgdir}/" install
    install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
    rmdir "${pkgdir}/usr/share/man/man5"
}

