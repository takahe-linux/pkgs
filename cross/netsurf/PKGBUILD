# Maintainer: Alastair Hughes < hobbitalastair at yandex dot com>

# Package information
pkgname='netsurf'
pkgver='3.6'
pkgrel=41
pkgdesc="Lightweight and fast web browser"

# Additional information...
arch=('mips' 'i586')
url="http://www.netsurf-browser.org/"
license=('MIT' 'GPL2')

# Dependency information
depends=('ca-certificates')
makedepends=('zlib' 'libpng' 'libjpeg' 'openssl' 'curl' 'musl' 'expat'
    'libcss' 'libdom' 'libhubbub' 'libwapcaplet' 'libparserutils'
    'libnsbmp' 'libnsfb' 'libnsgif' 'libutf8proc' 'libnsutils' 'sdl')
hostdepends=('git' 'toolchain' 'perl-html-parser' 'libpng'
    'netsurf-buildsystem' 'nsgenbind')

# Building information
source=("git://git.netsurf-browser.org/${pkgname}.git#tag=release/${pkgver}")
md5sums=('SKIP')


prepare() {
    cd "${srcdir}/${pkgname}"
    # TODO: Upstream; even in 1.3.1, libutf8proc does *not* install utf8proc.h
    # to libutf8proc/utf8proc.h.
    sed -i 'utils/idna.c' \
        -e 's:<libutf8proc/utf8proc.h>:<utf8proc.h>:' \
        -e 's:utf8proc_normalise:utf8proc_reencode:'

    # Make the default config.
    # TODO: PKG_CONFIG is being reset by Makefile; this should be fixed upstream?
    # TODO: Our pkg-config is obviously broken since PKG_CONFIG_SYSROOT_DIR
    #       wasn't being set anyway. Perhaps review what requires it and make
    #       it an explicit dependency.
    cat > Makefile.config << EOF
override PKG_CONFIG := PKG_CONFIG_SYSROOT_DIR=/sysroot ${_target_triplet}-pkg-config --static
EOF
}


build() {
    cd "${srcdir}/${pkgname}"
    make TARGET=framebuffer LIBDIR=lib INCLUDEDIR=include PREFIX=/usr
}


package() {
    cd "${srcdir}/${pkgname}"

    make TARGET=framebuffer LIBDIR=lib INCLUDEDIR=include PREFIX=/usr \
        DESTDIR="${pkgdir}/" install
    install -Dm644 COPYING \
        "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
