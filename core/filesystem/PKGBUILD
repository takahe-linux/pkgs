# Maintainer: Alastair Hughes < hobbitalastair at yandex dot com>

# Based off the Arch Linux PKGBUILD

# Package information
pkgname='filesystem'
pkgver='2014.11'
pkgrel=1
pkgdesc='Base filesystem'

# Additional information...
arch=('any')
url=''
license=('GPL')

# Dependency information
groups=('base')
depends=() # iana-etc in the Arch Linux PKGBUILD...

# Building information
backup=('etc/fstab'
        'etc/crypttab'
        'etc/group'
        'etc/hosts'
        'etc/ld.so.conf'
        'etc/passwd'
        'etc/shadow'
        'etc/gshadow'
        'etc/resolv.conf'
        'etc/motd'
        'etc/shells'
        'etc/host.conf'
        'etc/securetty'
        'etc/profile'
        'etc/issue')
install='filesystem.install'
source=('group'
        'issue'
        'securetty'
        'host.conf'
        'ld.so.conf'
        'passwd'
        'shadow'
        'fstab'
        'crypttab'
        'hosts'
        'motd'
        'resolv.conf'
        'shells'
        'gshadow'
        'profile'
        'locale.sh')
md5sums=('d46cb1adf8175888cc827a3dad8857d2'
         '7813c481156f6b280a3ba91fc6236368'
         '4c4540eeb748bf1f71d631b8c1dcf0b3'
         'f28150d4c0b22a017be51b9f7f9977ed'
         '6e488ffecc8ba142c0cf7e2d7aeb832e'
         'a998d1e6c7cc5791a7f4a2b49b2d10ce'
         '1ec1b16e2a71fad63ab8f147069a5d97'
         '693c97f2c9a519bb97a17008e92c2b74'
         '1745349eb24ed21b4cfaa6f423bddb76'
         '7bc65f234dfb6abf24e7c3b03e86f4ff'
         'd41d8cd98f00b204e9800998ecf8427e'
         '6f48288b6fcaf0065fcb7b0e525413e0'
         '22518e922891f9359f971f4f5b4e793c'
         '09bce9236c3729d31aaa528bcf5d198e'
         '0b6f835598c7640184e476057b3c7722'
         '4fab2b798deff348e04f05152111aae0')


package() {
    cd ${pkgdir}

    # Setup root filesystem
    for d in boot dev etc home usr var; do
        install -d -m755 ${d}
    done
    install -d -m555 proc
    install -d -m0750 root
    install -d -m1777 tmp

    # Setup /etc
    install -d etc/{ld.so.conf.d,skel,profile.d}
    for f in fstab group host.conf hosts issue ld.so.conf motd \
             passwd resolv.conf securetty shells profile; do
        install -m644 ${srcdir}/${f} etc/
    done

    ln -s /proc/self/mounts etc/mtab
    for f in gshadow shadow crypttab; do
        install -m600 ${srcdir}/${f} etc/
    done

    install -m755 ${srcdir}/locale.sh etc/profile.d/locale.sh

    # Setup /var
    for d in cache/man local opt log/old lib/misc empty; do
        install -d -m755 var/${d}
    done

    install -d -m1777 var/{tmp}
    ln -s spool/mail var/mail
    ln -s ../run var/run
    ln -s ../run/lock var/lock

    # Setup /usr hierarchy
    for d in bin include lib share/misc src; do
        install -d -m755 usr/${d}
    done
    for d in $(seq 8); do
        install -d -m755 usr/share/man/man${d}
    done
}
